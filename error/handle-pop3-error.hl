
/*
 * Invoked when an error occurs while "POP3 worker thread" was trying to fetch 
 * emails from POP3 server, and an error occurs.
 *
 * Expects [message], [stack-trace] and [type].
 *
 * Will retry connecting 3 times, and unless successful in any one of these 3
 * attempts, it will give up, and display an error message to the user.
 *
 * If it tries to automatically re-create the POP3 worker thread, this file will
 * return boolean "true", otherwise it will return "false".
 */





/*
 * Sanity checking invocation.
 */
micro.lambda.contract.min:x:/..
  message:string
  stack-trace:string
  type:string





/*
 * Checking how many times we have received an error in a row, and only if we
 * have had 3 errors in a row, we actually shut down the worker thread, and allows
 * user to see an error message, such that he can try to fix it.
 */
p5.web.session.get:sephia.core.pop3-error
if:x:/@p5.web.session.get/*?value
  not

  /*
   * This is the first time we invoke this error handler.
   */
  p5.web.session.set:sephia.core.pop3-error
    src:int:1

else

  /*
   * This is the n'th time we invoke this error handler.
   */
  p5.web.session.set:sephia.core.pop3-error
    +:x:/@p5.web.session.get/*?value.int
      _:1

if:x:/@p5.web.session.get/*?value.int
  !=:int:2

  /*
   * We should try to restart POP3 worker thread.
   *
   * Deleting thread signal object, making sure we've got synchronised access
   * to it, before we restart POP3 worker thread, and restart JavaScript "poller".
   */
  whoami

  /*
   * Synchronising access to thread signal object.
   */
  write-lock:sephia.core.thread-signal-{0}
    :x:/@whoami/*/username?value

    /*
     * Deleting thread signal object.
     */
    delete-data:x:/*/*/sephia.core.thread-signal-{0}
      :x:/@whoami/*/username?value

  /*
   * Restarting POP3 worker thread, and creating our JavaScript "poller".
   */
  sephia._internals.create-pop3-user-thread
  sephia._internals.create-poller-javascript

  /*
   * Returning true to caller to signal that we've tried to re-create our POP3 
   * worker thread.
   */
  return:bool:true
  




/*
 * Creating actual error message.
 */
create-widgets
  micro.widgets.modal:sephia-error-window
    class:micro-modal micro-modal-smaller
    widgets
      h3
        innerValue:Oops!
      p
        innerValue:And error of type '{0}' occurred, message from server was; '{1}'
          :x:/../*/type?value
          :x:/../*/message?value
      p
        innerValue:Hint, it might help to modify your settings ...
      div
        class:right
        widgets
          div
            class:strip
            style:"display:inline-block;"
            widgets
              button
                innerValue:Dismiss and restart
                onclick

                  /*
                   * Deleting error object, restarting thread worker, restarting JavaScript "poller", 
                   * and closing modal widget, making sure we've got synchronised access to thread 
                   * signal object.
                   */
                  whoami
                  write-lock:sephia.core.thread-signal-{0}
                    :x:/@whoami/*/username?value
                    delete-data:x:/*/*/sephia.core.thread-signal-{0}
                      :x:/@whoami/*/username?value
                  sephia._internals.create-pop3-user-thread
                  delete-widget:sephia-error-window
                  sephia._internals.create-poller-javascript

              button
                innerValue:Dismiss
                onclick

                  /*
                   * Closing modal widget.
                   */
                  delete-widget:sephia-error-window

              button
                innerValue:Open settings
                onclick

                  /*
                   * Closing modal widget.
                   */
                  delete-widget:sephia-error-window

                  /*
                   * Clearing out everything in main container widget of app.
                   */
                  clear-widget:sephia-main-container

                  /*
                   * Opening up settings.
                   */
                  create-widget
                    parent:sephia-main-container
                    widgets
                      div
                        class:row air-top
                        widgets
                          div
                            class:col
                            widgets
                              sephia._internals.widgets.settings





/*
 * Returning false to caller to signal that we gave up trying to re-create our
 * POP3 worker thread.
 */
return:bool:false
