/*
 * File responsible for starting Sephia Five, which means creating the main widget, 
 * and wire frame for displaying inbox, starting POP3 worker thread, etc.
 *
 * Notice, it will first force the user to setup his POP3 and SMTP settings, unless
 * he has already done so.
 */





/*
 * Creating main content container.
 *
 * This is the place where "everything" is stored in Sephia, and is used to display
 * the inbox, Sephia's settings, emails the user is reading, etc, etc, etc.
 *
 * Notice, we only use one "container" widget from Micro for everything related to Sephia.
 */
create-container-widget:sephia-main-container
  class:container





/*
 * Including Micro, and adding Awesome Fonts.
 */
micro.css.include





/*
 * Making sure user has an attachment folder.
 *
 * The first time the user opens Sephia, it will automatically create the user's
 * attachment folder, in addition to a temporary folder, where uploaded attachments will
 * be temporary stored, before an email is sent.
 *
 * Every time the user refresh Sephia's page, this temporary attachment folder will be emptied.
 */
micro.evaluate.file:@SEPHIA/init-user/ensure-user-attachment-folder.hl




/*
 * Making sure we delete all file in temporary attachment folder.
 */
list-files:@SEPHIA-USER-ATTACHMENTS/tmp/
delete-file:x:/-/*?name





/*
 * Retrieving user's settings, and making sure user has setup his account(s).
 *
 * Notice, if user hasn't setup his account(s), we won't allow him to open his inbox,
 * but force him to go through his settings, such as POP3, SMTP, PGP settings, etc.
 */
p5.auth.my-settings.get
if:x:/@p5.auth.my-settings.get/*/sephia
  not

  /*
   * Then creating our settings widget, before we return early.
   */
  create-widget
    parent:sephia-main-container
    widgets
      div
        class:row air-top
        widgets
          div
            class:col-100
            widgets
              div
                class:right
                widgets
                  div
                    class:strip toolbar
                    widgets
                      button
                        innerValue:@"<span class=""icon-home3""></span>"
                        onclick

                          /*
                           * Changing root URL of client to main root URL.
                           */
                          p5.web.get-root-location
                          p5.web.set-location:x:/-?value

          div
            class:col-100
            widgets
              sephia._internals.widgets.settings
                wizard-mode:true

  /*
   * Returning early to avoid evaluating the rest of this file.
   */
  return





/*
 * Including main CSS file for Sephia.
 *
 * Notice, this is done after all other CSS files are included, to make sure we
 * can override anything we wish in Sephia's main CSS file.
 */
p5.web.include-css-file:@SEPHIA/media/main.css





/*
 * Ensuring user has his "welcome email(s) and contact(s)".
 */
micro.evaluate.file:@SEPHIA/init-user/ensure-initial-welcome-emails-and-contacts.hl





/*
 * Making sure we start email fetching thread.
 *
 * This will by default create a thread for each user, that runs in the background,
 * and constantly pulls new emails from the user's POP3 server.
 *
 * Alternatively, it will check if user is allowed to fetch his emails, and if not,
 * return boolean "false".
 */
sephia._internals.create-pop3-user-thread





/*
 * Creating inbox widget.
 */
micro.evaluate.file:@SEPHIA/inbox/create-inbox.hl





/*
 * Creates widget that constantly polls server every n'th second to
 * check if new emails have arrived.
 */
micro.evaluate.file:@SEPHIA/inbox/create-client-server-poller.hl
