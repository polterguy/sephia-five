
/*
 * File responsible for starting Sephia Five, which means creating main widget, 
 * and wire frame for displaying inbox etc.
 */


/*
 * Including Micro, with Sea Breeze skin.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/ext.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@SEPHIA/media/main.css





/*
 * Making sure user has an attachment folder.
 */
if
  fetch:x:/0/0?value
    folder-exists:@SEPHIA-USER-ATTACHMENTS/
  not

  /*
   * Creating attachments folders, the "/tmp/" guy is for temporary attachments,
   * for instance attachments uploaded during composing an email, that still haven't
   * been sent, etc.
   */
  create-folder:@SEPHIA-USER-ATTACHMENTS/
  create-folder:@SEPHIA-USER-ATTACHMENTS/tmp/

else

  /*
   * Making sure we delete all file in temporary attachment folder.
   */
  list-files:@SEPHIA-USER-ATTACHMENTS/tmp/
  delete-file:x:/-/*?name





/*
 * Clearing out anything previously added to page.
 */
clear-widget:cnt
delete-widget-property:cnt
  class





/*
 * Creating main content container.
 */
create-container-widget:sephia-main-container
  class:container





/*
 * Retrieving user's settings, and making sure user has setup his account(s).
 * Notice, if user hasn't setup his account(s), we refuse him to open his inbox (for obvious reasons).
 */
p5.auth.my-settings.get
if:x:/@p5.auth.my-settings.get/*/sephia
  not

  /*
   * User has no settings, displaying settings form, and returning early.
   * Making sure we hide main menu widget.
   */
  create-widget:initial-settings
    parent:sephia-main-container
    widgets
      div
        class:row air-top
        widgets
          div
            class:col
            widgets
              sephia.widgets.settings
  return





/*
 * Making sure we start email fetching thread.
 *
 * This will create a thread for each user, that runs in the background,
 * and constantly pulls new emails from the user's POP3 server.
 */
load-file:@SEPHIA/core/fetch-emails.hl
eval:x:/-/*





/*
 * Creating main menu widget.
 */
create-widget:sephia-menu-container
  position:0
  class:container-fullscreen
  widgets
    micro.widgets.menu
      items
        Inbox
          onclick

            /*
             * Showing inbox.
             *
             * Notice, if inbox is already open, we simply re-databind the grid of emails,
             * to avoid clearing out any existingly read emails.
             */
            if
              fetch:x:/0/0?value
                widget-exists:sephia-inbox

              /*
               * Only re-databind grid.
               */
              sephia.inbox.open

            else

              /*
               * Need to load up inbox again in its entirety.
               */
              load-file:@SEPHIA/inbox/show.hl
              eval:x:/-/*

        Settings
          onclick

            /*
             * Clearing out everything in main container widget of app.
             */
            clear-widget:sephia-main-container

            /*
             * Opening up settings.
             */
            create-widget:initial-settings
              parent:sephia-main-container
              widgets
                div
                  class:row air-top
                  widgets
                    div
                      class:col
                      widgets
                        sephia.widgets.settings





/*
 * Opening inbox.
 */
load-file:@SEPHIA/inbox/show.hl
eval:x:/-/*





/*
 * Creates widget that constantly polls server every n'th second to
 * check if new emails have arrived.
 */
load-file:@SEPHIA/core/start-poller.hl
eval:x:/-/*
