
/*
 * Desktop widget file for Sephia Five.
 */
container
  element:a
  class:desktop-app shaded rounded air-inner bg
  title:Open emails
  oninit

    /*
     * Verifying user is logged in, and if not, deleting widget altogether, and returning early.
     */
    whoami
    if:x:/@whoami/*/default?value

      /*
       * User is not logged in, simply deleting entire icon.
       */
      delete-widget:x:/../*/_event?value
      return

    /*
     * Getting URL to point correctly.
     */
    p5.io.unroll-path:@SEPHIA/
    split:x:/-?value
      =:/
    set-widget-property:x:/../*/_event?value
      href:/{0}
        :x:/@split/0/-?name

  widgets
    span
      class:desktop-app-name
      innerValue:Email
      oninit

        /*
         * Making sure user is logged in, and if not, we don't display any count at all.
         */
        whoami
        if:x:/@whoami/*/default?value

          /*
           * User is not logged in, returning early.
           */
          return


        /*
         * Doing a count of unread emails for user, and adding to icon.
         */
        p5.mysql.connect:[sephia]
          p5.mysql.scalar:@"select count(*) from emails where username = @username and isread = 0 and type = 'received'"
            @username:x:/@whoami/*/username?value

          /*
           * Setting widget's innerValue, making sure we drop 'count' altogether, if it's zero.
           */
          if:x:/@p5.mysql.scalar?value.int
            =:int:0

            /*
             * There are no unread emails.
             */
            set-widget-property:x:/../*/_event?value
              innerValue:@"<span class=""icon-envelope desktop-app-icon""></span>"

          else

            /*
             * There's at least one unread email.
             */
            set-widget-property:x:/../*/_event?value
              innerValue:@"<span class=""icon-envelope desktop-app-icon""></span><span style=""position:absolute;top:5px;right:5px;font-size:.8rem;"">Sephia - {0}</span>"
                :x:/@p5.mysql.scalar?value
