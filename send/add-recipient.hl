
/*
 * Adds a recipient to some "input" type of widget from "contact" table.
 *
 * Expects [widget] being widget where to add result.
 */





/*
 * Forward declaring "return widget" to any who needs it.
 */
p5.io.unroll-path:@SEPHIA/
eval-x:x:/../**/_return-widget|/../*/create-widget/**/gaiasoul.camphora.widgets.grid-filter/*
create-widget
  class:add-recipient-widget
  after:x:/../*/widget?value
  widgets

    /*
     * Pager and filter wrapper.
     */
    div
      widgets

        /*
         * This is the "search and page" toolbar to filter and page in contacts.
         */
        gaiasoul.camphora.widgets.grid-filter
          databind-event:sephia.contacts.databind
          page-size:int:5
          search-accesskey:S
          class:row sephia-selector
          search-class:column column-50
          buttons-class:column column-50 sephia-add-recipient-button
          auto-focus:bool:true
          search-text:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-evil-search""></use></svg>"
            :x:/../*/p5.io.unroll-path?value
          clear-text:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-awesome-close""></use></svg>"
            :x:/../*/p5.io.unroll-path?value
          previous-text:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-evil-chevron-left""></use></svg>"
            :x:/../*/p5.io.unroll-path?value
          next-text:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-evil-chevron-right""></use></svg>"
            :x:/../*/p5.io.unroll-path?value
          widgets
            button
              innerValue:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-evil-close""></use></svg>"
                :x:/../*/p5.io.unroll-path?value
              title:Close
              onclick

                /*
                 * Forward declared during creation.
                 */
                _return-widget:x:/../*/widget?value

                /*
                 * Deleting search for recipient widget.
                 */
                p5.web.widgets.find-ancestor-like:x:/../*/_event?value
                  class:add-recipient-widget
                delete-widget:x:/@p5.web.widgets.find-ancestor-like/*/*?value
                p5.web.send-javascript:@"p5.$('{0}').el.focus();p5.$('{0}').el.select();"
                  :x:/@_return-widget?value
                p5.web.widgets.get-parent:x:/@_return-widget?value
                sys42.utilities.delete-css-classes:x:/@p5.web.widgets.get-parent/*/*?value
                  class:sephia-open-recipient

    /*
     * Table for displaying result.
     */
    table
      class:sephia-recipient-table
      oninit

        /*
         * Doing our initial databind, without any [filter] or [offset].
         */
        sephia.contacts.databind

      events

        /*
         * Custom "lambda events" for modal window.
         */
        sephia.contacts.databind

          /*
           * Cleaning out old records.
           */
          p5.web.widgets.find-first-ancestor-like:x:/../*/_event?value
            class:add-recipient-widget
          p5.web.widgets.find:x:/-/*/*?value
            element:tbody
          clear-widget:x:/@p5.web.widgets.find/*/*?value

          /*
           * Checking if a [filter] was specified, and if so, modifying our SQL in accordance.
           */
          if:x:/../*/filter?value

            /*
             * [filter] was provided, making sure we use it, by modifying the SQL, and adding a filter.
             */
            set:x:/../*/p5.mysql.connect/*/p5.mysql.select/[0,1]?value
              src:@"select * from contacts where username = @username and (name like @filter or email like @filter) order by name limit 5 offset @offset"
                @offset:x:/../*/offset?value.int
            add:x:/../*/p5.mysql.connect/*/p5.mysql.select/[0,1]
              src
                @filter:%{0}%
                  :x:/../*/filter?value

          /*
           * Defaulting [offset] to 0 if not given.
           */
          if:x:/../*/offset
            not
            insert-before:x:/../0
              src
                offset:int:0

          /*
           * Connecting to database and selecting 5 top records from "contact" table, sorted by name.
           */
          whoami
          p5.mysql.connect:[sephia]
            p5.mysql.select:@"select * from contacts where username = @username order by name limit 5 offset @offset"
              @username:x:/@whoami/*/username?value
              @offset:x:/../*/offset?value.int

            /*
             * Looping through above result set, creating a table row for each result.
             */
            for-each:x:/@p5.mysql.select/*
              create-widget
                element:tr
                parent:x:/@p5.web.widgets.find/*/*?value
                role:button
                _email:x:/@_dp/#/*/email?value
                onclick

                  /*
                   * Forward declared during creation.
                   */
                  _return-widget:x:/../*/widget?value

                  /*
                   * Adding email from current row into [widget] specified during creation, and closing window.
                   */
                  p5.web.widgets.property.get:x:/../*/_event?value
                    _email

                  /*
                   * Making sure email address is not already in recipient list.
                   */
                  p5.web.widgets.property.get:x:/@_return-widget?value
                    value
                  if
                    fetch:x:/0/0?value
                      index-of:x:/@p5.web.widgets.property.get/*/*?value
                        src:x:/@p5.web.widgets.property.get/@p5.web.widgets.property.get/*/*?value

                    /*
                     * Widget already contains clicked address, warning user, and returning early.
                     */
                    sys42.windows.info-tip:That address is already in your list of recipients.
                      class:info-window info-window-error
                    return

                  else-if:x:/@p5.web.widgets.property.get/*/*?value
                    =:

                    /*
                     * Setting widget's property.
                     */
                    p5.web.widgets.property.set:x:/@_return-widget?value
                      value:x:/@p5.web.widgets.property.get/@p5.web.widgets.property.get/*/*?value

                  else

                    /*
                     * Appending new address to widget.
                     */
                    p5.web.widgets.property.set:x:/@_return-widget?value
                      value:{0}, {1}
                        :x:/@p5.web.widgets.property.get/*/*?value
                        :x:/@p5.web.widgets.property.get/@p5.web.widgets.property.get/*/*?value

                  /*
                   * Destroying widget, setting focus to widget that had address appended into it.
                   */
                  p5.web.widgets.find-ancestor-like:x:/../*/_event?value
                    class:add-recipient-widget
                  delete-widget:x:/@p5.web.widgets.find-ancestor-like/*/*?value
                  p5.web.send-javascript:@"p5.$('{0}').el.focus();p5.$('{0}').el.select();"
                    :x:/@_return-widget?value
                  p5.web.widgets.get-parent:x:/@_return-widget?value
                  sys42.utilities.delete-css-classes:x:/@p5.web.widgets.get-parent/*/*?value
                    class:sephia-open-recipient

                widgets
                  td
                    innerValue:x:/@_dp/#/*/name?value
                  td
                    innerValue:x:/@_dp/#/*/email?value

      widgets

        /*
         * Widget where the contact items are inserted as rows.
         */
        tbody
          widgets
