
/*
 * Databinds our inbox datagrid.
 */
.defaults
  offset:int:0
  filter





/*
 * Needed to parametrize SQL select queries.
 */
whoami





/*
 * Opening database and selecting top 15 emails, sorted descendingly by date.
 */
p5.mysql.connect:[sephia]

  /*
   * Selecting 15 newest emails.
   */
  p5.mysql.select:@"select emails.id, subject, date, isread, type, contacts.email, contacts.name from emails inner join contacts on contacts.id = emails.sender 
where emails.username = @username and (subject like @filter or contacts.name like @filter or contacts.email like @filter 
or exists (select * from parts where content like @filter and parts.emailid = emails.id))
order by date desc limit 15 offset @offset"
    @username:x:/@whoami/*/username?value
    @offset:x:(/../*/offset|/../*/.defaults/*/offset)/$?value.int
    @filter:%{0}%
      :x:(/../*/filter|/../*/.defaults/*/filter)/$?value

  /*
   * Verifying that above SQL returned some items.
   */
  if:x:/@p5.mysql.select/*?count
    =:int:0
    return:bool:false





/*
 * Creating rows for our grid databind operation below.
 */
for-each:x:/@p5.mysql.connect/*/p5.mysql.select/*

  /*
   * Creating CSS class of row according to status of currently iterated email.
   */
  _class
  if:x:/@_dp/#/*/type?value
    =:sent

    /*
     * This email was sent by currently logged in user.
     */
    set:x:/@_class?value
      src:{0} sephia-sent
        :x:/@_class?value

  else-if:x:/@_dp/#/*/isread?value.int
    =:int:0

    /*
     * This email is still unread.
     */
    set:x:/@_class?value
      src:{0} sephia-unread
        :x:/@_class?value

  /*
   * Nicely formatting the date, and cleaning up class.
   */
  p5.types.date.format:x:/@_dp/#/*/date?value
    format:"d. MMM yyyy - HH:mm"
  set:x:/@_class?value
    trim:x:/@_class?value

  /*
   * Forward evaluating arguments, and creating our row.
   */
  eval-x:x:/+/*/*/*|/+/*/*/*/.row/*(/*/*/id|/title|/class|/.row-email-id)
  add:x:/../*/micro.widgets.grid.databind
    src
      item
        .row
          title:x:/@_dp/#/*/email?value
          class:x:/@_class?value
          .row-email-id:x:/@_dp/#/*/id?value
          onclick

            /*
             * Executing file responsible for reading a single email.
             */
            load-file:@SEPHIA/inbox/read.hl
            eval-x:x:/+/*
            eval:x:/-2/*
              id:x:/@_dp/#/*/id?value

        Name:x:/@_dp/#/*/name?value
        Subject:x:/@_dp/#/*/subject?value
        Date:x:/@p5.types.date.format?value


/*
 * Actual databind operation for grid.
 */
micro.widgets.grid.databind:sephia-inbox


/*
 * Returning true to signal that we actually had items, and were
 * able to re-databind grid.
 */
return:bool:true
