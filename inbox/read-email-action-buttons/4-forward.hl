
/*
 * Action button for replying to email
 */
button
  innerValue:@"<span class=""icon-mail-forward""></span>"
  title:Forward email
  onclick

    /*
     * Finding currently read email, to make sure we're able to inject
     * reply widget into page just beneath reader widget, before we delete
     * the reader widget itself.
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      .email-id
    get-widget-property:x:/-/*/*?value
      .email-id

    /*
     * Selecting email data from MySQL.
     */
    p5.mysql.connect:[sephia]
      p5.mysql.select:@"select subject, date, email, name from emails inner join contacts on emails.sender = contacts.id where emails.id = @id"
        @id:x:/@get-widget-property/*/*?value
      p5.mysql.select:@"select content, type from parts where emailid = @id"
        @id:x:/@get-widget-property/*/*?value

    /*
     * Making sure subject starts with "Fwd:"
     */
    set:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value
      src:"Fwd: {0}"
        :x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value

    /*
     * Appending forwarding information in front of content.
     */
    _content
    p5.types.date.format:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/date?value
      format:"ddd d. MMM yyyy, HH:mm"
    set:x:/@_content?value
      src:@"

--------- Forwarded message -----------
From: {0} <{1}>
Date: {2}
--------- Forwarded message -----------

{3}"
        :x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/name?value
        :x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/email?value
        :x:/@p5.types.date.format?value
        :x:/@p5.mysql.connect/*/p5.mysql.select/[1,2]/*/*/type/=plain/./*/content?value

    /*
     * HTML encoding content
     */
    set:x:/@_content?value
      p5.html.html-encode:x:/@_content?value


    /*
     * Creating actual reply widget.
     */
    eval-x:x:/+/**/sephia.widgets.compose-email/*
    create-widget
      after:x:/@p5.web.widgets.find-first-ancestor/*/*?value
      class:row air-top
      .email-id:x:/@get-widget-property/*/*?value
      widgets
        div
          class:col
          widgets

            /*
             * This is the actual "composer" widget, which allows user to compose/reply/etc an email.
             */
            sephia.widgets.compose-email
              subject:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value
              body:x:/@_content?value
              recipients
              class:air-inner shaded rounded
              .onclose

                /*
                 * Simply deleting main wrapper widget.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  .email-id
                delete-widget:x:/-/*/*?value

              .onsend

                /*
                 * Notifying user.
                 */
                micro.windows.info:Email was successfully sent

    /*
     * Deleting reader widget
     */
    delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value
