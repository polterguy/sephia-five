
/*
 * Action button for replying to email
 */
button
  innerValue:@"<span class=""icon-mail-reply-all""></span>"
  title:Reply to all
  class:reader-action-button
  oninit

    /*
     * Figuring out ID of email we're reading, to make sure we hide this button, in case the
     * email was sent by the user himself (in "outbox" that is).
     *
     * We also hide this button if email had only one recipient.
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      .email-id
    get-widget-property:x:/-/*/*?value
      .email-id

    /*
     * Selecting email type from MySQL.
     */
    p5.mysql.connect:[sephia]

      /*
       * Checking if email was sent by user.
       */
      p5.mysql.scalar:@"select type from emails where emails.id = @id"
        @id:x:/@get-widget-property/*/*?value
      p5.mysql.scalar:@"select count(*) from recipients where emailid = @id"
        @id:x:/@get-widget-property/*/*?value
      if:x:/..p5.mysql.connect/*/p5.mysql.scalar/[0,1]?value
        =:sent
        or:x:/..p5.mysql.connect/*/p5.mysql.scalar/[1,2]?value.int
          =:int:1

        /*
         * This email was sent by user himself, and hence replying to it makes no sense.
         */
        set-widget-property:x:/../*/_event?value
          visible:false

      else

        /*
         * Checking if email had multiple recipients.
         */
        p5.mysql.scalar:@"select count(*) from recipients where emailid = @id"
          @id:x:/@get-widget-property/*/*?value
        if:x:/@p5.mysql.scalar?value.int
          =:int:0

          /*
           * This email had no additional recipients, hence reply to all makes no sense.
           */
          set-widget-property:x:/../*/_event?value
            visible:false

  onclick

    /*
     * Finding currently read email, to make sure we're able to inject
     * reply widget into page just beneath reader widget, before we delete
     * the reader widget itself.
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      .email-id
    get-widget-property:x:/-/*/*?value
      .email-id

    /*
     * Getting "reply data" for email.
     */
    sephia._internals.get-reply-data:x:/@get-widget-property/*/*?value
      all:bool:true

    /*
     * Creating our reply widget.
     */
    add:x:/../*/micro.evaluate.file
      src:x:/@sephia._internals.get-reply-data/*
    eval-x:x:/+/*
    micro.evaluate.file:@SEPHIA/inbox/read-email-action-buttons/common/create-reply-widget.hl
      id:x:/@get-widget-property/*/*?value
      reader-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value
