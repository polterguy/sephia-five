
/*
 * Action button for replying to email
 */
button
  innerValue:@"<span class=""icon-mail-reply-all""></span>"
  title:Reply to all
  style:"margin-bottom:0;"
  oninit

    /*
     * Figuring out ID of email we're reading, to make sure we hide this button, in case the
     * email was sent by the user himself (in "outbox" that is).
     *
     * We also hide this button if email had only one recipient.
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      .email-id
    get-widget-property:x:/-/*/*?value
      .email-id

    /*
     * Selecting email type from MySQL.
     */
    p5.mysql.connect:[sephia]

      /*
       * Checking if email was sent by user.
       */
      p5.mysql.scalar:@"select type from emails where emails.id = @id"
        @id:x:/@get-widget-property/*/*?value
      p5.mysql.scalar:@"select count(*) from recipients where emailid = @id"
        @id:x:/@get-widget-property/*/*?value
      if:x:/..p5.mysql.connect/*/p5.mysql.scalar/[0,1]?value
        =:sent
        or:x:/..p5.mysql.connect/*/p5.mysql.scalar/[1,2]?value.int
          =:int:1

        /*
         * This email was sent by user himself, and hence replying to it makes no sense.
         */
        set-widget-property:x:/../*/_event?value
          visible:false

  onclick

    /*
     * Finding currently read email, to make sure we're able to inject
     * reply widget into page just beneath reader widget, before we delete
     * the reader widget itself.
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      .email-id
    get-widget-property:x:/-/*/*?value
      .email-id

    /*
     * Selecting email data from MySQL.
     */
    p5.mysql.connect:[sephia]
      p5.mysql.select:@"select subject, email from emails inner join contacts on emails.sender = contacts.id where emails.id = @id"
        @id:x:/@get-widget-property/*/*?value
      p5.mysql.select:@"select content, type from parts where emailid = @id"
        @id:x:/@get-widget-property/*/*?value
      p5.mysql.select:@"select type, contacts.email from recipients inner join contacts on recipients.contactid = contacts.id where recipients.emailid = @id"
        @id:x:/@get-widget-property/*/*?value

    /*
     * Making sure subject starts with "Re:"
     */
    if
      fetch:x:/0/0?value
        index-of:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value
          src:"Re:"
      !=:int:0

      /*
       * Subject didn't start with "Re:", prepending "Re: " in front of it.
       */
      set:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value
        src:"Re: {0}"
          :x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value

    /*
     * Quoting content of email by appending a ">" in front of each line in email content.
     */
    _content
    split:x:/@p5.mysql.connect/*/p5.mysql.select/[1,2]/*/*/type/=plain/./*/content?value
      =:"\r\n"
    join:x:/-/*?name
      sep:@"
>"
    set:x:/@_content?value
      src:">{0}"
        :x:/@join?value

    /*
     * HTML encoding content
     */
    set:x:/@_content?value
      p5.html.html-encode:x:/@_content?value


    /*
     * Adding "To" recipient from the "sender" parts of email.
     */
    add:x:/+/*
      src:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/email?value
    add:x:/../*/create-widget/**/sephia.widgets.compose/*/recipients/*/to
      src


    /*
     * Adding all other recipients.
     */
    for-each:x:/@p5.mysql.connect/*/p5.mysql.select/[2,3]/*
      if:x:/@_dp/#/*/type/=To
        add:x:/../*/create-widget/**/sephia.widgets.compose/*/recipients/*/to
          src:x:/@_dp/#/*/email?value
      else-if:x:/@_dp/#/*/type/=Cc
        add:x:/../*/create-widget/**/sephia.widgets.compose/*/recipients/*/cc
          src:x:/@_dp/#/*/email?value
      else-if:x:/@_dp/#/*/type/=Bcc
        add:x:/../*/create-widget/**/sephia.widgets.compose/*/recipients/*/bcc
          src:x:/@_dp/#/*/email?value


    /*
     * Removing "self" from recipients.
     */
    p5.auth.my-settings.get
    set:x:/../*/create-widget/**/sephia.widgets.compose/*/recipients/*/*/{0}
      :x:/@p5.auth.my-settings.get/*/sephia/*/email?value


    /*
     * Creating actual reply widget.
     */
    eval-x:x:/../*/create-widget/**/sephia.widgets.compose/*
    create-widget
      after:x:/@p5.web.widgets.find-first-ancestor/*/*?value
      class:row air-top
      .email-id:x:/@get-widget-property/*/*?value
      widgets
        div
          class:col
          widgets

            /*
             * This is the actual "composer" widget, which allows user to compose/reply/etc an email.
             */
            sephia.widgets.compose
              subject:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value
              body:x:/@_content?value
              recipients
                to
                cc
                bcc
              class:air-inner shaded rounded
              .onclose

                /*
                 * Simply deleting main wrapper widget.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  .email-id
                delete-widget:x:/-/*/*?value

              .onsend

                /*
                 * Notifying user.
                 */
                micro.windows.info:Email was successfully sent

    /*
     * Deleting reader widget
     */
    delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value
