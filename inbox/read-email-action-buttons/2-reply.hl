
/*
 * Action button for replying to email
 */
button
  innerValue:@"<span class=""icon-mail-reply""></span>"
  title:Reply to email
  style:"margin-bottom:0;"
  oninit

    /*
     * Figuring out ID of email we're reading, to make sure we hide this button, in case the
     * email was sent by the user himself (in "outbox" that is).
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      .email-id
    get-widget-property:x:/-/*/*?value
      .email-id

    /*
     * Selecting email data from MySQL.
     */
    p5.mysql.connect:[sephia]

      /*
       * Checking if email was sent by user.
       */
      p5.mysql.scalar:@"select type from emails where emails.id = @id"
        @id:x:/@get-widget-property/*/*?value
      if:x:/@p5.mysql.scalar?value
        =:sent

        /*
         * This email was sent by user himself, and hence replying to it makes no sense.
         */
        set-widget-property:x:/../*/_event?value
          visible:false

  onclick

    /*
     * Finding currently read email, to make sure we're able to inject
     * reply widget into page just beneath reader widget, before we delete
     * the reader widget itself.
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      .email-id
    get-widget-property:x:/-/*/*?value
      .email-id

    /*
     * Selecting email data from MySQL.
     */
    p5.mysql.connect:[sephia]
      p5.mysql.select:@"select subject, babelsubject, mimeid, email from emails inner join contacts on emails.sender = contacts.id where emails.id = @id"
        @id:x:/@get-widget-property/*/*?value
      p5.mysql.select:@"select content, type from parts where emailid = @id"
        @id:x:/@get-widget-property/*/*?value

    /*
     * Making sure subject starts with "Re:".
     */
    if
      fetch:x:/0/0?value
        index-of:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value
          src:"Re:"
      !=:int:0

      /*
       * Subject didn't start with "Re:", prepending "Re: " in front of it.
       */
      set:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value
        src:"Re: {0}"
          :x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value

    /*
     * Making sure "babel subject" starts with "Re:", but only if it actually exists.
     */
    if:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/babelsubject?value
      and
        fetch:x:/0/0?value
          index-of:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/babelsubject?value
            src:"Re:"
        !=:int:0

      /*
       * Babel subject didn't start with "Re:", prepending "Re: " in front of it.
       */
      set:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/babelsubject?value
        src:"Re: {0}"
          :x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/babelsubject?value

      /*
       * Adding explicit babelsubject to composer widget.
       */
      add:x:/../**/sephia.widgets.compose-email
        src
          babelsubject:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/babelsubject?value

    /*
     * Will contain the "massaged content" from email.
     */
    _content

    /*
     * Removing signature from email's content.
     */
    set:x:/@_content?value
      fetch:x:/0/0?name
        split:x:/@p5.mysql.connect/*/p5.mysql.select/[1,2]/*/*/type/=plain/./*/content?value
          =:"\r\n-- \r\n"

    /*
     * Quoting content of email by appending a ">" in front of each line in email content.
     */
    replace:x:/@_content?value
      src:"\r\n"
      dest:"\r\n> "
    set:x:/@_content?value
      src:"> {0}"
        :x:/@replace?value

    /*
     * HTML encoding content
     */
    set:x:/@_content?value
      p5.html.html-encode:x:/@_content?value


    /*
     * Adding "To" recipient.
     */
    add:x:/+/*/*
      src:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/email?value
    add:x:/../*/create-widget/**/sephia.widgets.compose-email/*/recipients
      src
        to


    /*
     * Creating actual reply widget.
     */
    eval-x:x:/+/**/sephia.widgets.compose-email(/*|/*/headers/*)
    create-widget
      after:x:/@p5.web.widgets.find-first-ancestor/*/*?value
      class:row air-top
      .email-id:x:/@get-widget-property/*/*?value
      widgets
        div
          class:col
          widgets

            /*
             * This is the actual "composer" widget, which allows user to compose/reply/etc an email.
             */
            sephia.widgets.compose-email
              markdown-composer:bool:true
              subject:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/subject?value
              body:x:/@_content?value
              recipients
              class:air-inner shaded rounded
              headers
                In-Reply-To:x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/mimeid?value
              .onclose

                /*
                 * Simply deleting main wrapper widget.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  .email-id
                delete-widget:x:/-/*/*?value

              .onsend

                /*
                 * Notifying user.
                 */
                micro.windows.info:Email was successfully sent

    /*
     * Deleting reader widget
     */
    delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value
