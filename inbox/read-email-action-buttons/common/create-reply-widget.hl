
/*
 * Creates a reply widget.
 *
 * Expects [reader-widget] being the ID of the widget that is creating our reply 
 * and [id] being the ID of the email we're replying to.
 * In addition to [subject], [content], [to] and optionally [babelsubject], [cc] and [bcc]
 */





/*
 * Adding recipients and custom MIME headers supplied by caller.
 */
add:x:/../*/create-widget/**/sephia.widgets.compose-email/*/recipients
  src:x:/../*(/to|/cc|/bcc)
add:x:/../*/create-widget/**/sephia.widgets.compose-email/*/headers
  src:x:/../*/headers/*





/*
 * Creating reply widget, making sure we forward evaluate all arguments first.
 */
eval-x:x:/../*/create-widget/**/sephia.widgets.compose-email/*
create-widget
  after:x:/../*/reader-widget?value
  class:row air-top
  .email-id:x:/../*/id?value
  widgets
    div
      class:col
      widgets

        /*
         * This is the actual "composer" widget, which allows user to compose/reply/etc an email.
         */
        sephia.widgets.compose-email
          markdown-composer:bool:true
          subject:x:/../*/subject?value
          body:x:/../*/content?value
          recipients
          class:air-inner shaded rounded
          headers
          .onclose

            /*
             * Simply deleting main wrapper widget.
             */
            p5.web.widgets.find-first-ancestor:x:/../*/_event?value
              .email-id
            delete-widget:x:/-/*/*?value
