
/*
 * Action button for verifying the sender's PGP fingerprint.
 */
button
  innerValue:@"<span class=""icon-smile-o""></span>"
  title:Verify contact's fingerprint
  class:reader-action-button
  oninit

    /*
     * Figuring out the ID of our email, such that we can figure out what type 
     * of email this is.
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      .email-id
    get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
      .email-id

    /*
     * Connecting to database, to retrieve contact from email, and possibly
     * hide this button, of there either are no fingerprint given in email,
     * or contact has already been verified.
     */
    p5.mysql.connect:[sephia]

      /*
       * Figuring out fingerprint of email, and if contact has been verified.
       */
      p5.mysql.select:@"select e.fingerprint, c.isverified from emails e inner join contacts c on e.sender = c.id where e.id = @id"
        @id:x:/@get-widget-property/*/*?value
      if:x:/@p5.mysql.select/*/*/isverified?value.int
        =:int:1
        or:x:/@p5.mysql.select/*/*/fingerprint?value
          not

        /*
         * This is not a spam email, hence we hide the button.
         */
        set-widget-property:x:/../*/_event?value
          visible:false

  onclick

    /*
     * Finding contact in database from email, and setting its verified property to true.
     */
    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
      .email-id
    get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
      .email-id

    /*
     * Opening database connection.
     */
    p5.mysql.connect:[sephia]

      /*
       * Selecting contact's ID.
       */
      p5.mysql.select:@"select sender, fingerprint from emails where id = @id"
        @id:x:/@get-widget-property/*/*?value

      /*
       * Updating contact.
       */
      eval-x:x:/+/*(/id|/fingerprint)
      sephia._internals.contacts.update
        id:x:/@p5.mysql.select/*/*/sender?value
        force:bool:true
        fingerprint:x:/@p5.mysql.select/*/*/fingerprint?value
        verified:bool:true

      /*
       * Re-databinding inbox.
       */
      sephia.inbox.databind
        keep-page-filter:bool:true

      /*
       * Removing "unknown" CSS class, and adding "safe" class to main reader
       * widget.
       */
      p5.web.widgets.find-first-like:x:/@p5.web.widgets.find-first-ancestor/*/*?value
        class:sephia-reader
      micro.css.delete:x:/@p5.web.widgets.find-first-like/*/*?value
        class:sephia-unknown
      micro.css.add:x:/@p5.web.widgets.find-first-like/*/*?value
        class:sephia-safe

      /*
       * Deleting button widget, since it's no longer required.
       */
      delete-widget:x:/../*/_event?value
