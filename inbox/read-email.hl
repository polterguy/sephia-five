
/*
 * Reads a single email.
 * Expects [id] being email id to read.
 */


/*
 * Checking if email is already opened in another widget.
 */
p5.web.widgets.find:sephia-main-container
  .email-id:x:/../*/id?value
if:x:/-/*/*?value

  /*
   * Another widget is already showing this email, returning early.
   */
  micro.windows.info:This email is already open
  return


/*
 * Opens database and selects data for email.
 */
p5.mysql.connect:[sephia]

  /*
   * Fetching attachments for email, and appending as buttons to email reader, which once
   * clicked will download the attachment.
   */
  p5.mysql.select:@"select * from attachments where emailid = @id"
    @id:x:/../*/id?value
  for-each:x:/@p5.mysql.select/*

    /*
     * Checking if this is a legal attachment.
     */
    eval-x:x:/+/*
    sephia.core._get-mime-type
      filename:x:/@_dp/#/*/filename?value
      prefix:x:/@_dp/#/*/prefix?value
      folder:x:/@_dp/#/*/folder?value
    if:x:/@sephia.core._get-mime-type/*

      /*
       * Attachment has a legal file extension according to configurtion of Sephia.
       * Adding [onclick] event handler for button, that will download attachment when clicked.
       */
      add:x:/..for-each/*/add/*/src/*/button
        src
          class:sephia-inbox-attachment
          onclick

            /*
             * Setting session variable to filename, and reloading location.
             * The launch.hl file will check for this session variable, and if found, instead
             * of reloading page, it will serve up the file requested.
             */
            get-widget-property:x:/../*/_event?value
              .folder
              .prefix
              .filename
            eval-x:x:/+/*/*/*
            p5.web.session.set:sephia.core.file-download
              src
                file
                  folder:x:/@get-widget-property/*/*/.folder?value
                  prefix:x:/@get-widget-property/*/*/.prefix?value
                  filename:x:/@get-widget-property/*/*/.filename?value
            p5.web.reload-location

    else

      /*
       * Attachment does not have a legal file extension.
       *
       * Warning user, before we allow him to download the file.
       */
      add:x:/..for-each/*/add/*/src/*/button
        src
          class:sephia-inbox-attachment sephia-inbox-illegal-attachment
          onclick

            /*
             * Displaying an information bubble, warning user about that file is not considered
             * safe, asking him if he is really sure he wants to download the file.
             */
            eval-x:x:/+/**/_widget
            micro.windows.info:These files are not considered safe, are you sure you wish to download it?
              class:micro-windows-info micro-windows-info-error
              widgets
                button
                  innerValue:Yes
                  style:"margin-bottom:0;margin-left:15px;"
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _widget:x:/../*/_event?value

                    /*
                     * Downloading file anyway.
                     */
                    get-widget-property:x:/../*/_widget?value
                      .folder
                      .prefix
                      .filename
                    eval-x:x:/+/*/*/*
                    p5.web.session.set:sephia.core.file-download
                      src
                        file
                          folder:x:/@get-widget-property/*/*/.folder?value
                          prefix:x:/@get-widget-property/*/*/.prefix?value
                          filename:x:/@get-widget-property/*/*/.filename?value
                    p5.web.reload-location
                

    /*
     * Adding button to reader for currently iterated attachment.
     */
    eval-x:x:/+/*/*/*
    add:x:/../*/create-widget/**/widgets/=attachments
      src
        button
          .folder::x:/@_dp/#/*/folder?value
          .prefix::x:/@_dp/#/*/prefix?value
          .filename::x:/@_dp/#/*/filename?value
          innerValue:x:/@_dp/#/*/filename?value

  /*
   * Fetching all recipients for email.
   */
  p5.mysql.select:@"select name, email, type from contacts 
inner join recipients on contacts.id = recipients.contactid
where recipients.emailid = @id"
    @id:x:/../*/id?value
  _to
  _cc
  _bcc

  /*
   * Creating recipients list, and putting into adequate node above.
   * This becomes a comma separated string for each of the above recipient types.
   */
  for-each:x:/@p5.mysql.select/*/*/type/=To/.
    set:x:/@_to?value
      src:{0}, {1} &lt;{2}&gt;
        :x:/@_to?value
        :x:/@_dp/#/*/name?value
        :x:/@_dp/#/*/email?value
  set:x:/@_to?value
    trim:x:/@_to?value
      chars:" ,"
  for-each:x:/@p5.mysql.select/*/*/type/=Cc/.
    set:x:/@_cc?value
      src:{0}, {1} &lt;{2}&gt;
        :x:/@_cc?value
        :x:/@_dp/#/*/name?value
        :x:/@_dp/#/*/email?value
  set:x:/@_cc?value
    trim:x:/@_cc?value
      chars:" ,"
  for-each:x:/@p5.mysql.select/*/*/type/=Bcc/.
    set:x:/@_bcc?value
      src:{0}, {1} &lt;{2}&gt;
        :x:/@_bcc?value
        :x:/@_dp/#/*/name?value
        :x:/@_dp/#/*/email?value
  set:x:/@_bcc?value
    trim:x:/@_bcc?value
      chars:" ,"

  /*
   * Retrieving actual email.
   */
  p5.mysql.select:@"select subject, date, signature, fingerprint, encrypted, type, name, email from emails 
inner join contacts on emails.sender = contacts.id 
where emails.id = @id"
    @id:x:/../*/id?value

  /*
   * This part will contain the background color of the reader, which
   * depends upon whether or not the email was encrypted or not.
   */
  _bg
  if:x:/@p5.mysql.select/*/*/encrypted?value.int
    !=:int:0
    and:x:/@p5.mysql.select/*/*/signature?value

    /*
     * Email was both encrypted and signed.
     */
    set:x:/@_bg?value
      src:success

  else

    /*
     * Email was either not encrypted or not signed.
     */
    set:x:/@_bg?value
      src:warning

  /*
   * This part will contain some of the "meta content" for email, more specifically who sent the email.
   */
  _meta1
  set:x:/@_meta1?value
    src:"From: {0} &lt;{1}&gt;"
      :x:/@p5.mysql.select/*/*/name?value
      :x:/@p5.mysql.select/*/*/email?value

  /*
   * This part will contain some more of the "meta content" for email, more specifically when it was sent.
   */
  _meta2
  p5.types.date.format:x:/@p5.mysql.select/*/*/date?value
    format:"dddd, d. MMMM yyyy, HH:mm"
  set:x:/@_meta2?value
    src:"Sent: {0}"
      :x:/@p5.types.date.format?value

  /*
   * This part will contain some more of the "meta content" for email, more specifically if email was sent encrypted.
   */
  _meta3
  if:x:/@p5.mysql.select/*/*/encrypted?value.int
    !=:int:0

    /*
     * Email was sent encrypted.
     */
    set:x:/@_meta3?value
      src:Email was sent encrypted

  else

    /*
     * Email was not encrypted
     */
    set:x:/@_meta3?value
      src:Email was not encrypted

  /*
   * This part will contain some more of the "meta content" for email, more specifically if email was cryptographically signed.
   */
  _meta4
  if:x:/@p5.mysql.select/*/*/signature?value

    /*
     * Email was cryptographically signed, making sure we HTML encode the signature User ID.
     */
    p5.html.html-encode:x:/@p5.mysql.select/*/*/signature?value
    set:x:/@_meta4?value
      src:"Signed by: {0} - {1}"
        :x:/@p5.html.html-encode?value
        :x:/@p5.mysql.select/*/*/fingerprint?value

  else

    /*
     * Email was not cryptographically signed
     */
    set:x:/@_meta4?value
      src:Email was not cryptographically signed

  /*
   * This part will contain all recipients of the email.
   */
  _meta5:@"Recipients: <ul style=""color:rgba(0,0,0,.3)"">"
  p5.mysql.select:@"select contacts.name, contacts.email from recipients inner join contacts on contacts.id = recipients.contactid where recipients.emailid = @id"
    @id:x:/../*/id?value
  for-each:x:/@p5.mysql.select/*
    set:x:/@_meta5?value
      src:"{0}<li> {1} &lt;{2}&gt;</li>"
        :x:/@_meta5?value
        :x:/@_dp/#/*/name?value
        :x:/@_dp/#/*/email?value
  set:x:/@_meta5?value
    src:{0}</ul>
      :x:/@_meta5?value

  /*
   * Selecting content of email.
   */
  p5.mysql.select:@"select * from parts where emailid = @id"
    @id:x:/../*/id?value

  /*
   * This part will contain the content to display to the user.
   */
  _content

  /*
   * Contains quoted parts.
   */
  _quote

  /*
   * Checking if a "plain/text" part exists for this email.
   */
  if:x:/@p5.mysql.select/*/*/type/=plain

    /*
     * Plain part exists.
     * Using regex to replace all URLs with hyperlinks.
     */
    p5.html.html-encode:x:/@p5.mysql.select/*/*/type/=plain/./*/content?value
    replace:x:/@p5.html.html-encode?value
      src:regex:@"/((http|ftp|https):\/\/[\w\-_]+(\.[\w\-_]+)+([\w\-\.,@?^=%&amp;:/~\+#]*[\w\-\@?^=%&amp;/~\+#])?)/c"
      dest:"<a target='_blank' href='$1'>$1</a>"
    split:x:/@replace?value
      =:@"
&gt;"
    set:x:/@_content?value
      src:x:/@split/0?name
    if:x:/@split/*?count
      >:int:1
      set:x:/@split/0
      join:x:/@split/*?name
        sep:@"
&gt;"
      set:x:/@_quote?value
        src:@"
&gt;{0}"
          :x:/@join?value
    else

      /*
       * No quoted parts, deleting "show quote button" and "quote content".
       */
      set:x:/../*/create-widget/**/widgets/=content/*(/button|/p/*/.quote/.)

  else

    /*
     * Only HTML part exists.
     * Stripping out everything that is considered "dangerous HTML".
     */
    p5.html.html2lambda:x:/@p5.mysql.select/*/*/type/=html/./*/content?value

    /*
     * Removing entire "head" part.
     */
    set:x:/@p5.html.html2lambda/*/*/head

    /*
     * Removing everything not in whitelist.
     */
    set:x:@"/@p5.html.html2lambda/*/**(!/body!/div!/table!/tr!/td!/th!/p!/span!/a!/strong!/br!/em!/img!/""\\#text""!/""\\@href"")"

    /*
     * Making sure we get "_blank" target for each hyperlink.
     */
    add:x:/@p5.html.html2lambda/*/**/a
      src
        @target:_blank

    /*
     * Transforming back to HTML.
     */
    p5.html.lambda2html:x:/@p5.html.html2lambda/*/*/body/*(!/~@)
    set:x:/@_content?value
      src:x:/@p5.html.lambda2html?value

    /*
     * No quoted parts, deleting "show quote button" and "quote content".
     */
    set:x:/../*/create-widget/**/widgets/=content/*(/button|/p/*/.quote/.)


  /*
   * Making sure we set email as read in database.
   */
  p5.mysql.update:@"update emails set isread = 1 where id = @id"
    @id:x:/../*/id?value


/*
 * Loading all action buttons.
 */
list-files:@SEPHIA/inbox/read-email-action-buttons/
  filter:.hl
load-file:x:/-/*?name
add:x:/../*/create-widget/**/widgets/=action-strip
  src:x:/@load-file/*/*


/*
 * Making sure email is displayed as read in grid.
 */
p5.web.widgets.find-like:sephia-main-container
  .row-email-id:x:/../*/id?value
get-widget-property:x:/-/*/*?value
  class
if:x:/-/*/*?value
  ~:sephia-unread
  micro.css.toggle:x:/@p5.web.widgets.find-like/*/*?value
    class:sephia-unread


/*
 * Creates actual widget showing email.
 */
create-widget
  parent:sephia-main-container
  class:row air-top
  .email-id:x:/../*/id?value
  widgets
    div
      class:col-100
      widgets
        div
          class:air-inner {0} shaded rounded
            :x:/@p5.mysql.connect/*/_bg?value
          widgets:content

            /*
             * Displays header of email.
             */
            h3
              innerValue:x:/@p5.mysql.connect/*/p5.mysql.select/[2,3]/*/*/subject?value

            /*
             * Displays body of email.
             */
            div
              style:"white-space:pre-wrap;width:100%;overflow:hidden;"
              innerValue:x:/@p5.mysql.connect/*/_content?value

            /*
             * Displays attachments of email.
             */
            container
              class:sephia-inbox-attachments
              widgets:attachments

            /*
             * Allows user to show "quoted parts" of email.
             */
            button
              innerValue:...
              class:air-top
              onclick

                /*
                 * Displays quoted parts of email.
                 */
                p5.web.widgets.get-parent:x:/../*/_event?value
                p5.web.widgets.find:x:/-/*/*?value
                  .quote
                set-widget-property:x:/-/*/*?value
                  visible:true
                delete-widget:x:/../*/_event?value

            /*
             * Displays quoted parts of email.
             *
             * This part is initially hidden.
             */
            p
              .quote
              style:"white-space:pre-wrap;"
              visible:false
              innerValue:x:/@p5.mysql.connect/*/_quote?value

            /*
             * Container wrapping "meta data" of email.
             *
             * This parts is initially shown invisible, and only displayed when user clicks the action button
             * that explicitly says he wants to see these parts.
             */
            container
              visible:false
              .meta-data
              widgets

                /*
                 * A simple separator between the email content, and its meta data.
                 */
                hr
                  style:"margin-top:1rem;"

                /*
                 * Displays additional "meta data" about email, more specifically the date email was sent.
                 */
                p
                  style:"color:rgba(0,0,0,.3);font-style:italic;margin-bottom:0;"
                  innerValue:x:/@p5.mysql.connect/*/_meta2?value

                /*
                 * Displays email "meta data", more specifically who sent the email.
                 */
                p
                  style:"color:rgba(0,0,0,.3);font-style:italic;margin-bottom:0;"
                  innerValue:x:/@p5.mysql.connect/*/_meta1?value

                /*
                 * Displays additional "meta data" about email, more specifically if email was sent encrypted.
                 */
                p
                  style:"color:rgba(0,0,0,.3);font-style:italic;margin-bottom:0;"
                  innerValue:x:/@p5.mysql.connect/*/_meta3?value

                /*
                 * Displays additional "meta data" about email, more specifically if email was cryptographically signed.
                 */
                p
                  style:"color:rgba(0,0,0,.3);font-style:italic;margin-bottom:0;"
                  innerValue:x:/@p5.mysql.connect/*/_meta4?value

                /*
                 * Displays additional "meta data" about email, more specifically all recipients.
                 */
                p
                  style:"color:rgba(0,0,0,.3);font-style:italic;"
                  class:air-top
                  innerValue:x:/@p5.mysql.connect/*/_meta5?value

            /*
             * "Action strip" containing all buttons for actions user can do with email, such as "Reply", "Forward", "Delete", etc.
             */
            div
              class:right
              widgets
                div
                  class:strip
                  style:"display:inline-block;"
                  widgets:action-strip





/*
 * Updating page title, since we now (probably) have read one more email.
 */
load-file:@SEPHIA/inbox/update-page-title.hl
eval:x:/-/*





/*
 * Informing user he has to scroll to the bottom of the page to find his email.
 */
micro.windows.info:Your email can be found at the bottom of the page
