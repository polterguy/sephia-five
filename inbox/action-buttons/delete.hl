
/*
 * Action button for deleting email.
 */
button
  innerValue:Delete
  style:"margin-bottom:0;"
  onclick

    /*
     * Asking user to confirm action using a modal widget.
     */
    eval-x:x:/+/**/_widget
    create-widgets
      micro.widgets.modal:sephia-delete-email-confirmation-window
        class:micro-modal micro-modal-smaller
        widgets
          h3
            innerValue:Warning!
          p
            innerValue:Warning, this action is permanent. Are you sure you wish to proceed?
          div
            class:right
            widgets
              div
                class:strip
                style:"display:inline-block;"
                widgets
                  button
                    innerValue:Yes
                    oninit
                      micro.page.set-focus:x:/../*/_event?value
                    onclick

                      /*
                       * Deleting widgets wrapping currently read email and email from database.
                       */
                      _widget:x:/../*/_event?value
                      p5.web.widgets.find-first-ancestor:x:/../*/_widget?value
                        .email-id
                      get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                        .email-id
                      p5.mysql.connect:[sephia]
                        p5.mysql.delete:@"delete from emails where id = @id"
                          @id:x:/@get-widget-property/*/*?value
                        p5.mysql.select:@"select mimeid from emails where username = @username order by id desc limit 1"
                          @username:x:/@whoami/*/username?value
                      delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value

                      /*
                       * Deleting modal confirmation widget.
                       */
                      delete-widget:sephia-delete-email-confirmation-window

                      /*
                       * Databinding inbox again.
                       */
                      sephia.inbox.open
                        keep-page-filter:bool:true

                      /*
                       * Making sure we update "last known" ViewState value, such that user is not given notification about
                       * "new email" in case this was the last received email.
                       */
                      p5.web.viewstate.set:sephia.emails.last-known
                        src:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/mimeid?value

                  button
                    innerValue:No
                    onclick

                      /*
                       * Deleting widgets wrapping currently read email.
                       */
                      _widget:x:/../*/_event?value
                      p5.web.widgets.find-first-ancestor:x:/../*/_widget?value
                        .email-id
                      delete-widget:x:/-/*/*?value

                      /*
                       * Deleting modal confirmation widget.
                       */
                      delete-widget:sephia-delete-email-confirmation-window
