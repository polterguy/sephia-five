
/*
 * Creates the initial welcome emails for user, that welcomes him to Sephia Five,
 * and gives him some hints about how he can use Sephia.
 *
 * In addition, it will also insert the user's initial contacts.
 *
 * Both of these parts are configurable, and can be found in Sephia's "/config/" folder.
 */





/*
 * Opening up database connection.
 */
p5.mysql.connect:[sephia]

  /*
   * Needed to parametrize SQL queries and insertions.
   */
  whoami

  /*
   * Before we start our transaction, which has some cost to it - We check if user
   * has already any existing emails, and if so, we simply return early, since user
   * has already started using Sephia earlier, and probably have received these welcoming
   * emails previously.
   */
  p5.mysql.select:@"select count(*) from emails where username = @username"
    @username:x:/@whoami/*/username?value
  if:x:/-?value.int
    >:int:0

    /*
     * User has already started using Sephia Five previously, returning early.
     */
    return

  /*
   * Needed further below.
   */
  p5.auth.my-settings.get

  /*
   * Creating transaction object, to avoid having partial emails inserted 
   * into our database.
   */
  p5.mysql.transaction.begin

    /*
     * Then loading up all default contacts, and inserting them into database for user.
     */
    load-file:@SEPHIA/config/initial-contacts.hl

    /*
     * Looping through results of above file, and inserting a new contact for each entry.
     */
    for-each:x:/@load-file/*/*/contact

      /*
       * Parametrizing and invoking [sephia._internals.contacts.create].
       */
      add:x:/..for-each/*/sephia._internals.contacts.create
        src:@"username:{0}"
          :x:/@whoami/*/username?value
      add:x:/..for-each/*/sephia._internals.contacts.create
        src:x:/@_dp/#/*
      sephia._internals.contacts.create

    /*
     * Loading configuration file that contains the initial welcoming email user
     * receives when he first starts Sephia Five.
     */
    load-file:@SEPHIA/config/welcome-emails.hl

    /*
     * Making sure we insert our "To" parts into all welcome emails our configuration 
     * file contains, such that the currently logged in user becomes the recipient of
     * all "welcome emails".
     */
    add:x:/+/*/*
      src:@"{0}:{1}"
        :x:/@p5.auth.my-settings.get/*/sephia/*/name?value
        :x:/@p5.auth.my-settings.get/*/sephia/*/email?value
    insert-after:x:/@load-file/*/*/envelope/*/From
      src
        To

    /*
     * Needed to create an accurate date of when user "received" his welcome email,
     * which paradoxically becomes his inital usage date.
     */
    p5.types.date.now

    /*
     * Looping through each "welcome email" in our configuration file.
     */
    for-each:x:/@load-file/*/*/envelope

      /*
       * Inserting currently iterated email, by invoking file responsible for 
       * doing that. Making sure we forward evaluate all arguments.
       */
      add:x:/..for-each/*/micro.evaluate.file
        src:x:/@_dp/#
      eval-x:x:/..for-each/*/micro.evaluate.file/*
      micro.evaluate.file:@SEPHIA/core/save-pop3-email-2-database.hl
        username:x:/@whoami/*/username?value

    /*
     * Committing transaction.
     */
    p5.mysql.transaction.commit

