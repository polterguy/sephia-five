
/*
 * Creates the main GUI structure of Sephia Five.
 *
 * Sets a couple of properties on the page, in addition to creating the main "root widgets", such as the
 * "inbox widget" and the "toolbar widget", for then to initially populate the "inbox datagrid".
 */





/*
 * Unrolling path, which is necessary for databinding search toolbar buttons [innerValue] towards SVG file.
 */
p5.io.unroll-path:@SEPHIA/





/*
 * Creating "retriever" widget.
 *
 * Notice, this widget serves a single purpose, which is to retrieve the emails from our POP3 server,
 * and put them into our database.
 *
 * We could have stuffed this guy into another widget, but since we delete widgets, according to where we
 * are in the program, this seems to be easier.
 */
create-widget:sephia-retriever
  style:"display:none;"

  /*
   * This Ajax event is invoked from JavaScript on page, whenever we should for some reasons check POP3 server
   * if it has more emails we need to fetch.
   */
  .onretrieve

    /*
     * Error handling fetch operation.
     */
    try

      /*
       * This file simply retrieves the next batch of emails from our POP3 server, and stores them into our
       * MySQL database.
       */
      sys42.utilities.execute-lambda-file:@SEPHIA/inbox/helpers/fetch.hl

      /*
       * Checking if there was any new unread emails on POP server, and if so, we check to see
       * if datagrid exists on page, and if both of these conditions are true, we re-create the datagrid, reflecting
       * the new items fetched from the POP server.
       */
      if:x:/@sys42.utilities.execute-lambda-file?value
        and
          fetch:x:/0/0?value
            widget-exists:sephia-datagrid
        clear-widget:sephia-datagrid
        sys42.utilities.execute-lambda-file:@SEPHIA/inbox/show.hl

    catch
      sys42.windows.info-tip:@"Something went wrong when trying to check for new emails, please check your settings, or internet connection. 
Message from server was '{0}'"
        :x:/@message?value
        class:info-window info-window-error info-window-even-longer





/*
 * Creating header and "menu" row.
 */
eval-x:x:/../*/create-widget/*/widgets/*/gaiasoul.camphora.widgets.grid-filter/*
create-widget:sephia-header
  class:container
  widgets

    /*
     * Header at the top, feel free to add whatever "branding" widgets you wish here, replacing the existing widget.
     */
    div:sephia-main-header
      class:row sephia-brand
      widgets
        a
          href:"https://gaiasoul.com"
          target:_blank
          class:column
          widgets
            h1:sephia-header-text
              innerValue:@"Sephia Five"
    /*
     * This is the "search and page" toolbar at the top.
     *
     * Notice, we pass in "sephia-selector" CSS class to widget, to make it easy to style.
     */
    gaiasoul.camphora.widgets.grid-filter
      databind-event:sephia.inbox.databind
      search-accesskey:F
      class:row sephia-selector
      search-class:column column-50
      buttons-class:column column-50
      search-text:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-evil-search""></use></svg>"
        :x:/../*/p5.io.unroll-path?value
      clear-text:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-ant-cross""></use></svg>"
        :x:/../*/p5.io.unroll-path?value
      previous-text:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-evil-chevron-left""></use></svg>"
        :x:/../*/p5.io.unroll-path?value
      next-text:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-evil-chevron-right""></use></svg>"
        :x:/../*/p5.io.unroll-path?value
      widgets

        /*
         * Compose new email button.
         */
        button
          innerValue:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-ant-mail""></use></svg>"
            :x:/../*/p5.io.unroll-path?value
          onclick

            /*
             * Creating a new email, and making sure we scroll widget for creating it into view.
             */
            sys42.utilities.execute-lambda-file:@SEPHIA/send/create.hl
            p5.web.widgets.find:sephia-read-emails
              _email-id:x:/../*/email-id?value.string
            p5.web.send-javascript:@"p5.$('{0}').el.scrollIntoView();"
              :x:/@p5.web.widgets.find/*/*?value

        /*
         * Open settings button
         */
        button
          innerValue:@"<svg class=""sephia-icon""><use xlink:href=""{0}media/si-sprite.svg#si-ant-setting""></use></svg>"
            :x:/../*/p5.io.unroll-path?value
          onclick

            /*
             * Opening up "configure settings form", making sure we populate fields with existing settings.
             */
            delete-widget:sephia-header
            delete-widget:sephia-content
            sys42.utilities.execute-lambda-file:@SEPHIA/configure/configure.hl
              change:bool:true





/*
 * Creating "content" widget.
 */
create-widget:sephia-content
  class:container
  events

    /*
     * Event raised when datagrid needs re-databinding.
     *
     * Expects optionally [offset] and [filter], simply passes these two values into evaluation of Hyperlambda file,
     * responsible for re-databinding "inbox".
     */
    sephia.inbox.databind

      /*
       * Invoking file responsible for re-databinding "inbox".
       *
       * First making sure we delete old widgets.
       */
      clear-widget:sephia-datagrid
      add:x:/+
        src:x:/../*(/filter|/offset)
      sys42.utilities.execute-lambda-file:@SEPHIA/inbox/show.hl

  oninit

    /*
     * Making sure during initial page load, that we initiate checking for new emails after 1 minute.
     */
    p5.web.send-javascript:@"setTimeout (function() {p5.$('sephia-retriever').raise('.onretrieve')}, 5000);"

  widgets

    /*
     * This is our "datagrid", which displays all the emails currently being viewed by user.
     */
    div
      class:row
      widgets
        container
          class:column
          widgets
            table
              class:sephia-inbox
              widgets
                thead
                  innerValue:@"<tr><th>From</th><th>Subject</th><th>Date</th><th>Status</th></tr>"

                /*
                 * "Datagrid" content (inbox).
                 */
                container:sephia-datagrid
                  element:tbody

    /*
     * Wrapper for "reading emails".
     *
     * This widget will contain any emails the user has selected for reading and/or replying to.
     */
    container:sephia-read-emails
      class:sephia-reader





/*
 * Showing inbox.
 */
sys42.utilities.execute-lambda-file:@SEPHIA/inbox/show.hl
