
/*
 * Will search through the currently logged in user's list of contacts.
 *
 * Pass in [_arg], and optionally [max] being an integer number,
 * declaring how many contacts you'd like the event to maximumly return, 
 * and [offset] being the offset you'd like to start having contacts returned from.
 *
 * [max] defaults to 10 if not given.
 *
 * Will return one [row] for each contact matching your query, having an [id],
 * [name], [email], [fingerprint], [isverified], [type] and [spam] for each row.
 */
create-event:sephia.contacts.search

  /*
   * Defaults value of arguments.
   */
  .defaults
    max:int:10
    offset:int:0

  /*
   * Sanity checking optional arguments.
   */
  micro.lambda.contract.optional:x:/..
    _arg:string
    max:int
    offset:int

  /*
   * Needed to parametrise SQL commands.
   */
  whoami

  /*
   * Opening connection, and performing the search.
   */
  p5.mysql.connect:[sephia]

    /*
     * Selecting top x contacts from database.
     */
    p5.mysql.select:@"select c.* from contacts c
where (c.name like @query or c.email like @query) 
and c.username = @username
order by c.name
limit @max offset @offset"
      :x:/@_extra?value
      @query:"%{0}%"
        :x:/../*/_arg?value
      @username:x:/@whoami/*/username?value
      @max:x:(/../*/max|/../*/.defaults/*/max)/$?value.int
      @offset:x:(/../*/offset|/../*/.defaults/*/offset)/$?value.int

    /*
     * Returning results to caller.
     */
    add:x:/+
      src:x:/@p5.mysql.select/*/row
    return
