
/*
 * Will submit the given [fingerprint] public key to your key server.
 * Expects the [fingerprint] key to exist in your GnuPG database.
 *
 * Returns boolean true if upload was successful, otherwise false.
 */
create-event:sephia.pgp.key-server.submit

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    fingerprint:string

  /*
   * Retrieving public key.
   */
  p5.crypto.get-public-key:x:/../*/fingerprint?value
  if:x:/-/*
    not
    throw:That key doesn't exist in your GnuPG database

  /*
   * URL encoding key, althought technically this is probablt not necessary.
   */
  p5.html.url-encode:x:/@p5.crypto.get-public-key/*?value

  /*
   * Loading key server configuration setting.
   */
  load-file:@SEPHIA/configuration/key-server.hl

  /*
   * HTTP POST/PUT'ing public key to key server, making sure we use the
   * right HTTP verb, and that we are adding the "keytext" correctly as 
   * an argument to our HTTP REST invocation.
   */
  add:x:/../*/p5.http.xxx
    src:x:/@load-file/*/*/upload-key-url/*(!/http-verb!/key-arg-name)
  set:x:/../*/p5.http.xxx?name
    src:p5.http.{0}
      :x:/@load-file/*/*/upload-key-url/*/http-verb?value
  p5.http.xxx:x:/@load-file/*/*/upload-key-url?value
    content:"{0}={1}"
      :x:/@load-file/*/*/upload-key-url/*/key-arg-name?value
      :x:/@p5.html.url-encode?value

  /*
   * Checking status.
   */
  if:x:/@p5.http.post/*/result/*/status?value
    =:OK

    /*
     * Success!
     */
    return:bool:true

  else

    /*
     * Error!
     */
    return:bool:false
