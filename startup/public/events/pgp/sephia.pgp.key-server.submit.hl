
/*
 * Will submit the given public key to the key server from your config settings.
 *
 * Expects the fingerprint of the key to submit to be supplied as [_arg].
 *
 * Returns boolean true if upload was successful, otherwise false.
 */
create-event:sephia.pgp.key-server.submit

  /*
   * Retrieving public key.
   */
  p5.crypto.get-public-key:x:/../*/_arg?value
  p5.html.url-encode:x:/@p5.crypto.get-public-key/*?value

  /*
   * Loading key server configuration setting.
   */
  load-file:@SEPHIA/config/key-server.hl

  /*
   * HTTP POST/PUT'ing public key to key server, making sure we use the
   * right HTTP verb, and that we are adding the "keytext" correctly as 
   * an argument to our HTTP REST invocation.
   */
  add:x:/../*/p5.http.xxx
    src:x:/@load-file/*/*/upload-key-url/*(!/http-verb!/key-arg-name)
  set:x:/../*/p5.http.xxx?name
    src:p5.http.{0}
      :x:/@load-file/*/*/upload-key-url/*/http-verb?value
  p5.http.xxx:x:/@load-file/*/*/upload-key-url?value
    content:"{0}={1}"
      :x:/@load-file/*/*/upload-key-url/*/key-arg-name?value
      :x:/@p5.html.url-encode?value

  /*
   * Checking status.
   */
  if:x:/@p5.http.post/*/result/*/status?value
    =:OK

    /*
     * Success!
     */
    return:bool:true

  else

    /*
     * Error!
     */
    return:bool:false
