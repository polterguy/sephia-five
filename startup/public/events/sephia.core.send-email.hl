
/*
 * Creates our "send email" event.
 *
 * Requires [subject], [body], [to] and optionally [cc] and [bcc], which all should be a comma separated list of emails.
 * Optionally pass in any number of [attachment] nodes, being attachments to send with email.
 * Any [attachment] nodes given should have values pointing to files on the server.
 * You can also pass in an optional [babelsubject], which will explicitly use this subject, instead of creating a 
 * new babel subject.
 *
 * [to], [cc] and [bcc] should be comma separated list of emails.
 *
 * You can also pass in an optional [headers] collection, which will be MIME headers associate with the message.
 *
 * Notice, unless you pass in [force], and set its value to boolean "true", the email will not
 * be sent unless all recipients are in user's list of contacts, but instead the event will return "false" to
 * the caller.
 *
 * Will save the sent email into database, associated with the user who sent it.
 *
 * If the email is successfully sent, boolean "true" will be returned to caller.
 */
create-event:sephia.core.send-email

  /*
   * Sanity check, verifies that at least [subject], [body] and [to] is given.
   */
  micro.lambda.contract.min:x:/..
    subject:string
    body:string
    to:string

  /*
   * Sanity checking optional arguments.
   */
  micro.lambda.contract.optional:x:/..
    cc:string
    bcc:string
    babelsubject:string
    attachment:string
    headers
    force

  /*
   * Retrieving settings for currently logged in user, and keeping the [whoami] object
   * around since it's needed later.
   */
  whoami
  p5.auth.my-settings.get

  /*
   * Used to hold envelope data for email.
   */
  .envelope
    To
    Cc
    Bcc
    From
    Subject
    body

  /*
   * Used to temporary hold fingerprints of all recipients of email.
   */
  .fingerprints

  /*
   * Used to temporary hold attachments for email, since we'll need them in a 
   * different format at the end of this event, when saving email to database.
   */
  .attachments

  /*
   * Adding all recipients' email addresses
   */
  add:x:/@.envelope/*/To
    split:x:/../*/to?value
      =:,
      =:" "
  add:x:/@.envelope/*/Cc
    split:x:/../*/cc?value
      =:,
      =:" "
  add:x:/@.envelope/*/Bcc
    split:x:/../*/bcc?value
      =:,
      =:" "

  /*
   * Removing any of [To], [Cc] and [Bcc] which does not contain children nodes.
   */
  set:x:/@.envelope/*(/Cc|/Bcc)(!/*/.)

  /*
   * Doing a lookup for each contact's name, and setting the correct node name above accordingly.
   */
  p5.mysql.connect:[sephia]
    for-each:x:/@.envelope/*(/To|/Cc|/Bcc)/*
      p5.mysql.select:@"select name, fingerprint from contacts where username = @username and email = @email"
        @username:x:/@whoami/*/username?value
        @email:x:/@_dp/#?name 

      /*
       * Verifying contact exists, and if not, inserting new contact.
       */
      if:x:/@p5.mysql.select/*
        not

        /*
         * Currently iterated recipient did not exist in user's list of contacts.
         *
         * Checking if caller wants to [force] sending email, and unless he does,
         * we do not send the email, but return "false" to caller.
         */
        if:x:/../*/force
          not
          or:x:/../*/force?value.bool
            =:bool:false
          return:bool:false

        /*
         * Inserting a new contact.
         */
        eval-x:x:/+/*
        sephia._internals.contacts.create
          name:
          email:x:/@_dp/#?name
          username:x:/@whoami/*/username?value

        /*
         * For simplicity, adding return value from above invocation into original SQL query results.
         */
        eval-x:x:/+/*/*/*
        add:x:/@p5.mysql.select
          src
            row
              name:x:/@sephia._internals.contacts.create/*/name?value
              fingerprint:x:/@sephia._internals.contacts.create/*/fingerprint?value

      /*
       * Notice, we first switch the name of the node to become its value,
       * before we set the name of the node to the contact's name from our database.
       */
      set:x:/@_dp/#?value
        src:x:/@_dp/#?name
      set:x:/@_dp/#?name
        src:x:/@p5.mysql.select/*/*/name?value

      /*
       * Storing fingerprint of contact for later reference, if contact has a fingerprint.
       */
      if:x:/@p5.mysql.select/*/*/fingerprint?value
        add:x:/@.fingerprints
          src:x:/@p5.mysql.select/*/*/fingerprint?value

  /*
   * Setting "From" of email.
   */
  add:x:/@.envelope/*/From
    src:@"{0}:{1}"
      :x:/@p5.auth.my-settings.get/*/sephia/*/name?value
      :x:/@p5.auth.my-settings.get/*/sephia/*/email?value

  /*
   * Adding subject to envelope.
   */
  set:x:/@.envelope/*/Subject?value
    src:x:/../*/subject?value

  /*
   * Then any custom [headers] caller requested to have associated with email.
   */
  add:x:/@.envelope
    src:x:/../*/headers/*

  /*
   * Used to track if envelope has been given a multipart or not.
   *
   * We try to keep construction of email as small as possible, and only
   * create a multipart if we really have to.
   *
   * This makes sure that unless user both have attachments and has chosen to
   * compose HTML emails (using Markdown) - Then only a single text:plain MIME type
   * will be created.
   */
  .has-multipart:bool:true

  /*
   * Then attachments.
   */
  for-each:x:/../*/attachment

    /*
     * Checking if attachment's file extension is a legal type of attachment.
     */
    if
      sephia._internals.is-legal-attachment:x:/@_dp/#?value

      /*
       * Checking if we need to create a multipart for envelope.
       */
      if:x:/@.has-multipart?value

        /*
         * We'll need to create a root multipart MIME entity.
         */
        set:x:/@.has-multipart?value
          src:bool:false
        add:x:/@.envelope/*/body
          src
            multipart:mixed

        /*
         * Since this is our first attachment, we verify that the user has an attachment folder.
         */
        micro.evaluate.file:@SEPHIA/core/ensure-user-attachment-folder.hl

      /*
       * Copy file into attachment folder, and figuring out new path 
       * for file from attachments folder.
       *
       * This is done to make sure we have all email attachments in
       * the same folder, which implies that we'll need to create a unique
       * filename, which is why we're using the [prefix], which is simply
       * a random GUID. Without this random GUID, we could run the risk
       * of having multiple attachments with the same filename overwrite
       * each other.
       *
       * Hence, an attachment file has three parts in our database.
       *
       * - [folder] being the folder, which is normally @SEPHIA-USER-ATTACHMENTS.
       * - [prefix] being a random GUID.
       * - [filename] being the filename of the attachment.
       */
      p5.io.unroll-path:@SEPHIA-USER-ATTACHMENTS/
      p5.types.guid.new
      split:x:/@_dp/#?value
        =:/
      copy-file:x:/@_dp/#?value
        dest:{0}{1}-{2}
          :x:/@p5.io.unroll-path?value
          :x:/@p5.types.guid.new?value
          :x:/@split/0/-?name

      /*
       * Adding file to above [.attachments] lambda, which is needed later
       * when saving email to database.
       */
      eval-x:x:/+/*/**
      add:x:/@.attachments
        src
          file:x:/@split/0/-?name
            prefix:{0}-
              :x:/@p5.types.guid.new?value
            folder:x:/@p5.io.unroll-path?value

      /*
       * Figuring out MIME type for file.
       */
      eval-x:x:/+/*
      sephia._internals.mime.get-type-from-file-extension
        folder:x:/@p5.io.unroll-path?value
        prefix:x:/@p5.types.guid.new?value
        filename:x:/@split/0/-?name

      /*
       * Adding file to [.envelope].
       */
      add:x:/@.envelope/*/body/*/multipart
        src:x:/@sephia._internals.mime.get-type-from-file-extension/*

    else

      /*
       * Attachment was not a legal type of attachment in Sephia.
       *
       * Trying to give user a sane and readable error message.
       */
      throw:@"File ""{0}"" is not supported by Sephia Five"
        :x:/@split/@split/0/-?name

  /*
   * Checking if we should create an HTML view, by converting content of email into Markdown.
   */
  if:x:/@p5.auth.my-settings.get/*/sephia/*/markdown-composer?value

    /*
     * User's settings requested that Markdown should be used.
     */
    sephia.core.markdown.to-html:x:/../*/body?value
    eval-x:x:/+/*/*/*/*/content

    /*
     * Notice, this expression might look "funny", but our email structure depends upon
     * whether or not user has added attachments or not.
     *
     * If user has attached files in his email, then a "multipart:mixed" wrapper around
     * the entire email will have been created. We prioritize adding our "multipart:alternative"
     * entity inside of this wrapper multipart.
     * If this wrapper "multipart:mixed" doesn't exist however, we simply make sure our 
     * "multipart:alternative" becomes the root MIME entity of our message.
     *
     * Notice, to avoid trailing spaces being stripped in lines of text, which are used in
     * among other places signatures, etc - We need to encode the text:plain content such 
     * that these trailing SP characters are not removed. QuotedPrintable will do just that.
     */
    add:x:(/@.envelope/*/body/*/multipart|/@.envelope/*/body)/[0,1]
      src
        multipart:alternative
          text:plain
            Content-Transfer-Encoding:QuotedPrintable
            content:x:/../*/body?value
          text:html
            content:x:/@sephia.core.markdown.to-html?value

  else

    /*
     * Caller did not want to create an HTML view.
     */
    eval-x:x:/+/*/*/*/content

    /*
     * Notice, this expression might look "funny", but our email structure depends upon
     * whether or not user has added attachments or not.
     *
     * See above comments for explanation.
     *
     * QuotedPrintable encoding for content is necessary to avoid having MimeKit remove trailing
     * SP characters in lines of text, which are used in e.g. signatures etc.
     */
    add:x:(/@.envelope/*/body/*/multipart|/@.envelope/*/body)/[0,1]
      src
        text:plain
          Content-Transfer-Encoding:QuotedPrintable
          content:x:/../*/body?value

  /*
   * Adding cryptography keys for all recipients,
   * if we found public PGP keys for all recipients.
   */
  p5.crypto.list-public-keys:x:/@.fingerprints/*?name
  if:x:/-/*?count
    =:x:/@.envelope/*(/To|/Cc|/Bcc)/*?count

    /*
     * We've got cryptography keys for all recipients.
     *
     * First adding an [encrypt] node to our root MIME entity, which might be either
     * a "multipart:mixed", "multipart:alternative" or a simple "text:plain", depending
     * upon whether or not user has added attachments to email, chosen to create alternative 
     * HTML views by converting text content into Markdown, or neither of the above.
     *
     * Hence, at this point, we shouldn't care what type of MIME entity is our root entity,
     * and simply choose to encrypt the root MIME entity, regardless of its type.
     */
    add:x:/@.envelope/*/body/0
      src:encrypt

    /*
     * Then adding up our [fingerprint] to [encrypt] node we created above.
     */
    for-each:x:/@.fingerprints/*?name
      eval-x:x:/+/*/*
      add:x:/@.envelope/*/body/0/*/encrypt
        src
          fingerprint:x:/@_dp?value

  /*
   * Then checking if email should be signed.
   */
  if:x:/@p5.auth.my-settings.get/*/sephia/*/pgp-key?value
    and:x:/@p5.auth.my-settings.get/*/sephia/*/pgp-key?value
      !=:

    /*
     * Email should (possibly) be signed.
     *
     * Before we know for sure, we must check up if the private PGP key exists in GnuPG database.
     */
    p5.crypto.list-private-keys:x:/@p5.auth.my-settings.get/*/sephia/*/pgp-key?value
    if:x:/-/*

      /*
       * Private key for user exists in GnuPG database.
       *
       * Notice, our root MIME entity might be either a "multipart:mixed", "multipart:alternative" 
       * or a simple "text:plain", depending upon whether or not user has added attachments to email, 
       * chosen to create alternative HTML views by converting text content into Markdown, or neither 
       * of the above.
       *
       * Hence, at this point, we shouldn't care what type of MIME entity is our root entity,
       * and simply choose to sign the root MIME entity, regardless of its type.
       */
      eval-x:x:/+/*/*/**
      add:x:/@.envelope/*/body/0
        src
          sign
            fingerprint:x:/@p5.auth.my-settings.get/*/sephia/*/pgp-key?value
              password:x:/@p5.auth.my-settings.get/*/sephia/*/pgp-password?value

  /*
   * Now we have created our message, and we can send it and save it to our database.
   *
   * Notice though, that when we send our message, we actually remove its subject, and 
   * add it as a MIME header named "X-Subject" inside of our innermost multipart, if
   * the email is supposed to be sent encrypted.
   *
   * This is done to hide the actual subject of our email, and have its real subject be encrypted,
   * while using a randomly generated subject outside of our encrypted parts.
   *
   * First we add entire [.envelope] into SMTP invocation.
   */
  eval-x:x:/../*/p5.smtp.send/*
  add:x:/../*/p5.smtp.send/*/envelope
    src:x:/@.envelope/*

  /*
   * Then we add our envelope into our [sephia._internals.smtp.save-envelope] invocation, which
   * will save our email to our database.
   */
  add:x:/../*/sephia._internals.smtp.save-envelope/*
    src:x:/@.envelope/*
  add:x:/../*/sephia._internals.smtp.save-envelope
    src:x:/@.attachments/*

  /*
   * Babeling our subject, but only if email is to be sent encrypted.
   *
   * Notice, this is done on the data that now exists inside of [p5.smtp.send],
   * and will change the subject line, and move it into our encrypted parts of email.
   */
  if:x:/../*/p5.smtp.send/*/envelope/*/body/0/*/encrypt

    /*
     * Email is be sent encrypted, "babelizing" our subject.
     *
     * First base64 encoding real subject, and adding it to encrypted multipart entity as 
     * a custom MIME header called "X-Subject".
     *
     * This MIME header is added to the root MIME entity of our message, regardless of what 
     * type of MIME entity that is.
     */
    p5.string.encode-base64:x:/../*/subject?value
    eval-x:x:/+/*/*
    insert-before:x:/../*/p5.smtp.send/*/envelope/*/body/0/0
      src
        X-Subject:x:/@p5.string.encode-base64?value

    /*
     * Then getting a new random subject, and exchanging actual subject with "babel subject", or
     * if supplied, using the babel subject specified when invoking event.
     */
    if:x:/../*/babelsubject?value

      /*
       * Caller supplied an explicit babel subject.
       */
      set:x:/../*/p5.smtp.send/*/envelope/*/Subject?value
        src:x:/../*/babelsubject?value

    else

      /*
       * Caller did not supply an explicit babel subject.
       *
       * Retrieving babelized subject from babelizer.
       */
      sephia._internals.smtp.get-babel-subject
      set:x:/../*/p5.smtp.send/*/envelope/*/Subject?value
        src:x:/@sephia._internals.smtp.get-babel-subject?value

    /*
     * Then to support email clients that does not support our custom "X-Subject" MIME header,
     * we move subject line into both of its text parts.
     *
     * First we move subject into our "text:plain" MIME part's content.
     *
     * Subject is added as a Markdown "h3" element at the top of our email in our "text:plain" part.
     */
    set:x:/../*/p5.smtp.send/*/envelope/**/text/=plain/*/content?value
      src:@"### {0}

{1}"
        :x:/../*/subject?value
        :x:/../*/p5.smtp.send/*/envelope/**/text/=plain/*/content?value

    /*
     * Then we move our subject into our "text:html" MIME part's content, if there is one.
     *
     * This is done by semantically parsing our HTML view's content, and inserting our own "h3" element
     * directly into the HTML, using the [html2lambda] and [lambda2html] events.
     */
    if:x:/../*/p5.smtp.send/*/envelope/**/text/=html

      /*
       * There is an HTML part, hence we'll need to insert an "h3" element into the top 
       * of our "body" element.
       */
      html2lambda:x:/../*/p5.smtp.send/*/envelope/**/text/=html/*/content?value
      eval-x:x:/+/*/*/*
      insert-before:x:/@html2lambda/**/body/0
        src
          h3
            #text:x:/../*/subject?value

      /*
       * Then converting results of above insertion back into HTML, and updating the "text:html"
       * MIME entity of our email.
       */
      lambda2html:x:/@html2lambda/*
      set:x:/../*/p5.smtp.send/*/envelope/**/text/=html/*/content?value
        src:x:/@lambda2html?value

  /*
   * Sending our actual email using our user's SMTP settings.
   */
  p5.smtp.send
    server:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-server?value
    port:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-port?value
    ssl:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-ssl?value
    username:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-username?value
    password:x:/@p5.auth.my-settings.get/*/sephia/*/smtp-password?value
    envelope

  /*
   * Saves entire outgoing [envelope] to database.
   *
   * Notice, contrary to what goes to our SMTP server, this [envelope] is saved as it
   * actually is, without our "babel subject".
   *
   * But we'll need to make sure the email is saved with the correct MIME ID, which is returned
   * from above invocation to [p5.smtp.send].
   */
  set:x:/+/*/envelope?value
    src:x:/@p5.smtp.send/*/envelope?value
  sephia._internals.smtp.save-envelope
    envelope

  /*
   * Email was successfully sent, returning "true" to caller.
   */
  return:bool:true
