
/*
 * Returns the MIME type for a file name, in addition to the "Content-Transfer-Encoding" if necessary.
 *
 * Requires [file], [folder] and [prefix].
 *
 * If filename supplied as [file] is a non-supported file type, meaning its file extension is not valid for a Sephia attachment,
 * it will return nothing.
 * Otherwise it will return the MIME type, and Content-Disposition, in addition to Content-Transfer-Encoding unless it's plain text.
 */
create-event:sephia.core._get-mime-type

  /*
   * Splitting filename given in [file], to figure out its extension.
   */
  split:x:/../*/file?value
    =:.
  load-file:@SEPHIA/config/mime-types.hl
  if:x:/@load-file/*/*/{0}
    :x:/@split/0/-?name

    /*
     * Supported file type, first adding MIME type to [return].
     */
    add:x:/../*/return
      src:x:/@load-file/*/*/{0}/*
        :x:/@split/0/-?name

    /*
     * Adding "Content-Disposition" MIME header.
     */
    eval-x:x:/+/*/*
    add:x:/../**/return/*
      src
        Content-Disposition:@"attachment; filename=""{0}"""
          :x:/../*/file?value
        filename:{0}{1}-{2}
          :x:/../*/folder?value
          :x:/../*/prefix?value
          :x:/../*/file?value

  /*
   * Return invocation is dynamically populated in above code, if file specified is a supported MIME file extension.
   */
  return
