
/*
 * Returns the MIME type for a file name, in addition to the "Content-Transfer-Encoding".
 *
 * If filename supplied as [_arg] is a non-supported file type, meaning its file extension is not valid for a Sephia attachment,
 * it will return nothing.
 * Otherwise it will return the MIME type, and Content-Disposition, in addition to Content-Transfer-Encoding unless it's plain text.
 */
create-event:sephia.core._get-mime-type

  /*
   * Forward evaluating all [Content-Disposition] and [filename] return args.
   */
  split:x:/../*/_arg?value
    =:/

  /*
   * Splitting filename given in [_arg], to figure out its extension.
   */
  split:x:/../*/_arg?value
    =:.
  load-file:@SEPHIA/config/mime-types.hl
  if:x:/@load-file/*/*/{0}
    :x:/@split/0/-?name

    /*
     * Supported file type, first adding MIME type to [return].
     */
    add:x:/../*/return
      src:x:/@load-file/*/*/{0}/*
        :x:/@split/0/-?name

    /*
     * Adding "Content-Disposition" MIME header.
     */
    split:x:/../*/_arg?value
      =:/
    eval-x:x:/+/*/*
    add:x:/../**/return/*
      src
        Content-Disposition:@"attachment; filename=""{0}"""
          :x:/@split/0/-?name
        filename:x:/../*/_arg?value

  /*
   * Return invocation is dynamically populated in above code, if file specified is a supported MIME file extension.
   */
  return
