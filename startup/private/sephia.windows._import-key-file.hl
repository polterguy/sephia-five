
/*
 * Shows a modal window with the details about a public PGP key found at the [_arg] location,
 * asking user if he wants to import the key into his PGP database, and associate it with the 
 * given [contact].
 *
 * [contact] is expected to be the ID of a contact from your database.
 */
create-event:sephia.windows._import-key-file

  /*
   * Figuring out contact's email, name and existing fingerprint (if any).
   */
  p5.mysql.connect:[sephia]
    p5.mysql.select:@"select name, email, fingerprint from contacts where id = @id"
      @id:x:/../*/contact?value

  /*
   * Loading public PGP key.
   */
  load-file:x:/../*/_arg?value
  p5.crypto.preview-public-pgp-key:x:/-/*?value

  /*
   * Checking if the fingerprint for our key is the same as the one already registered with this contact,
   * and if so, we return early since there's nor reasons to proceed, since we've alread got this key.
   */
  if:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/fingerprint?value
    =:x:/@p5.crypto.preview-public-pgp-key/0?name

    /*
     * No changes, returning early.
     */
    return

  /*
   * Creating modal widget, making sure we forward evaluate any args.
   */
  eval-x:x:/+/**(/_contact|/_filename)|/+/**/tbody/**
  create-widgets
    micro.widgets.modal:sephia-import-pgp-key
      class:micro-modal micro-modal-smaller
      widgets
        h3
          innerValue:Import PGP key?
        p
          innerValue:@"We found a public PGP key in email, do you wish to import it into your GnuPG 
database and associate it with your contact; <strong>{0} &lt;{1}&gt;</strong>?"
            :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/name?value
            :x:/@p5.mysql.connect/*/p5.mysql.select/*/*/email?value
        table
          widgets
            tbody
              widgets
                tr
                  widgets
                    td
                      innerValue:Fingerprint
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0?name
                tr
                  widgets
                    td
                      innerValue:ID
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/id?value
                tr
                  widgets
                    td
                      innerValue:Algorithm
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/algorithm?value
                tr
                  widgets
                    td
                      innerValue:Strength
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/strength?value
                tr
                  widgets
                    td
                      innerValue:Created
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/creation-time?value
                tr
                  widgets
                    td
                      innerValue:Can encrypt
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/is-encryption-key?value
                tr
                  widgets
                    td
                      innerValue:Is master
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/is-master-key?value
                tr
                  widgets
                    td
                      innerValue:Is revoked
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/is-revoked?value
                tr
                  widgets
                    td
                      innerValue:Expires
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/expires?value
                tr
                  widgets
                    td
                      innerValue:User ID
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/user-ids/0?value
                tr
                  widgets
                    td
                      innerValue:Signees
                    td
                      innerValue:x:/@p5.crypto.preview-public-pgp-key/0/*/signed-by/*?count
        div
          class:right
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets
                button
                  innerValue:Yes, and I have verified this key!
                  oninit
                    micro.page.set-focus:x:/../*/_event?value
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _contact:x:/../*/contact?value
                    _filename:x:/../*/_arg?value

                    /*
                     * Importing key into GnuPG database and associating it with the specified [contact].
                     */
                    load-file:x:/@_filename?value
                    p5.crypto.import-public-pgp-key:x:/-/*?value
                    p5.mysql.connect:[sephia]
                      p5.mysql.update:@"update contacts set fingerprint = @fingerprint, isverified = 1 where id = @id"
                        @fingerprint:x:/@p5.crypto.import-public-pgp-key/0?name
                        @id:x:/@_contact?value

                    /*
                     * Deleting modal widget.
                     */
                    delete-widget:sephia-import-pgp-key

                button
                  innerValue:Yes
                  onclick

                    /*
                     * Forward evaluated above.
                     */
                    _contact:x:/../*/contact?value
                    _filename:x:/../*/_arg?value

                    /*
                     * Importing key into GnuPG database and associating it with the specified [contact].
                     */
                    load-file:x:/@_filename?value
                    p5.crypto.import-public-pgp-key:x:/-/*?value
                    p5.mysql.connect:[sephia]
                      p5.mysql.update:@"update contacts set fingerprint = @fingerprint, isverified = 0 where id = @id"
                        @fingerprint:x:/@p5.crypto.import-public-pgp-key/0?name
                        @id:x:/@_contact?value

                    /*
                     * Deleting modal widget.
                     */
                    delete-widget:sephia-import-pgp-key

                button
                  innerValue:No
                  onclick

                    /*
                     * Closing widget without taking any further actions.
                     */
                    delete-widget:sephia-import-pgp-key
