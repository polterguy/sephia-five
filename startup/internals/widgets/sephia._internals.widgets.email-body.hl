
/*
 * Extension widget that creates the widget that displays the body of an email.
 *
 * Expects [row] from "parts" MySQL table.
 */
create-event:sephia._internals.widgets.email-body

  /*
   * Signal node, to be able to easily find arguments.
   */
  .signal

  /*
   * Sanity checking invocation.
   */
  micro.lambda.contract.min:x:/..
    row
      type:string
      content:string
      markdown:int

  /*
   * Checking email part type, which must be "plain/text" to make Sephia able
   * to display it.
   */
  if:x:/@.signal/--(!/_arg)/*/type/=plain
    not
    and:x:/@.signal/--(!/_arg)/*/type/=html

    /*
     * Email only contains an HTML view.
     *
     * Trying our best to show it.
     */
    sephia._internals.whitewash-html:x:/@.signal/--(!/_arg)/*/type/=html/[0,1]/./*/content?value
    eval-x:x:/+/*/*
    return
      literal
        element:div
        class:sephia-reader-content
        innerValue:x:/@sephia._internals.whitewash-html?value

  else-if:x:/@.signal/--(!/_arg)/*/type/=plain
    not

    /*
     * We have no idea how to display this email...!!
     */
    return
      literal
        element:div
        class:sephia-reader-content sephia-reader-content-error
        innerValue:@"<p>Sorry, we couldn't display this email, since it contains only HTML parts.
Please notify your contacts of that you prefer for them to send you plain text emails. HTML emails, are mostly used to spread marketing, in addition to that they're a security risk.</p>"

  /*
   * Checking if email can be converted to Markdown.
   */
  if:x:/@.signal/--(!/_arg)/*/type/=plain/[0,1]/./*/markdown?value.int
    =:int:1

    /*
     * Email is actually Markdown, converting its content to Markdown, and returning
     * a widget wrapping its HTML content, instead of its plain text content.
     */
    sephia._internals.markdown-to-html:x:/@.signal/--(!/_arg)/*/type/=plain/[0,1]/./*/content?value

    /*
     * Returning Markdown to caller.
     */
    eval-x:x:/+/*/*
    return
      literal
        element:div
        class:sephia-reader-content
        innerValue:x:/@sephia._internals.markdown-to-html?value

  /*
   * HTML encoding email to avoid HTML tag injection.
   */
  p5.html.html-encode:x:/@.signal/--(!/_arg)/*/type/=plain/[0,1]/./*/content?value

  /*
   * Making sure we transform all URLs in email to become clickable hyperlinks.
   */
  sephia._internals.html.create-hyperlinks:x:/@p5.html.html-encode?value

  /*
   * Returning pre-formatted "div" element wrapping content of email.
   */
  eval-x:x:/+/*/*
  return
    literal
      element:div
      class:sephia-reader-content
      innerValue:x:/@sephia._internals.html.create-hyperlinks?value
