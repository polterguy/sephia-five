
/*
 * Creates a contact from the given [name], [email] and [username] arguments, 
 * and returns the ID of the contact that was inserted.
 *
 * Optionally pass in [fingerprint], which is the fingerprint of the public PGP key
 * the contact should be created with.
 *
 * Will attempt to find a public PGP key for contact, either in GnuPG database, or using key server.
 * Notice, expects an open database connection to the Sephia database.
 */
create-event:sephia._internals.contacts.create

  /*
   * Making sure invoker obeys by lambda contract.
   */
  micro.lambda.contract.min:x:/..
    name:string
    email:string
    username:string

  /*
   * Sanity checking optional arguments.
   */
  micro.lambda.contract.optional:x:/..
    fingerprint:string

  /*
   * Making sure name defaults to first parts of email, if it is empty or null.
   */
  if:x:/../*/name?value
    =:
    split:x:/../*/email?value
      =:@
    set:x:/../*/name?value
      src:x:/@split/0?name

  /*
   * Used to hold any fingerprints of public PGP key matching email.
   */
  _fingerprint

  /*
   * Checking if there exists a public PGP key for user in GnuPG database.
   */
  p5.crypto.list-public-keys:x:(/../*/fingerprint|/../*/email)/[0,1]?value
  if:x:/-/*?count
    >:int:0

    /*
     * We found a potentially matching key for contact in GnuPG database.
     */
    set:x:/@_fingerprint?value
      src:x:/@p5.crypto.list-public-keys/0?name

  else-if:x:/../*/fingerprint?value

    /*
     * Caller supplied a [fingerprint] argument, downloading key if it exists.
     */
    sephia.pgp.key-server.download-key:x:/../*/fingerprint?value
    p5.crypto.import-public-pgp-key:x:/@sephia.pgp.key-server.download-key?value
    set:x:/@_fingerprint?value
      src:x:/@p5.crypto.import-public-pgp-key/0?name

  else

    /*
     * No matching key in GnuPG, trying to search on key server, making sure we wrap
     * our attempt into a try/catch block.
     */
    try
      sephia.pgp.key-server.search:x:/../*/email?value
      if:x:/@sephia.pgp.key-server.search/*?count
        >:int:0

        /*
         * We found a key on key server, downloading it, and importing it into GnuPG database.
         */
        sephia.pgp.key-server.download-key:x:/@sephia.pgp.key-server.search/0?value
        p5.crypto.import-public-pgp-key:x:/@sephia.pgp.key-server.download-key?value
        set:x:/@_fingerprint?value
          src:x:/@p5.crypto.import-public-pgp-key/0?name

    catch

      /*
       * Silently catching, since invocation might be from a different thread.
       */

  /*
   * Inserting new contact into database.
   */
  p5.mysql.insert:@"insert into contacts (name, email, username, fingerprint) values (@name, @email, @username, @fingerprint)"
    @name:x:/../*/name?value
    @email:x:/../*/email?value
    @username:x:/../*/username?value
    @fingerprint:x:/@_fingerprint?value

  /*
   * Returning newly inserted contact to caller.
   */
  eval-x:x:/+/*
  return
    id:x:/@p5.mysql.insert/*/id?value
    name:x:/../*/name?value
    email:x:/../*/email?value
    username:x:/../*/username?value
    fingerprint:x:/@_fingerprint?value
  
