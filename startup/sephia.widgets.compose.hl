
/*
 * Extension widget for composing an email.
 * Optionally you can pass in [subject], [body], [recipients][to], [recipients][cc] and [recipients][bcc].
 *
 * You can also optionally pass in [.onclose] and [.onsend] lambda callbacks, which will be invoked when
 * email composer is closed and/or email is sent, in addition to [class], which will be the root class
 * of the composer.
 */
create-event:sephia.widgets.compose

  /*
   * Default values for arguments not supplied.
   */
  .defaults
    class:air-inner

  /*
   * Including JavaScript necessary to handle uploading of attachments.
   */
  p5.web.include-javascript:@"
p5.sephia_upload_files = function(e) {
  var files = e.target.files;
  for(var i = 0; i < files.length; i++) {
    var reader = new FileReader();
    reader.onload = (function(file) {
      return function(data) {
        p5.$(e.target.id).raise('.onupload', {
          onbefore:function(pars,evt) {
            pars.push(['sephia-file-name', file.name], ['sephia-file-content', data.target.result.split('base64,')[1]]);
          }
        });
      }
    })(files[i]);
    reader.readAsDataURL(files[i]);
  }
}
"

  /*
   * Adding lambda callbacks for both [.onclose] and [.onsend].
   */
  add:x:/../*/return/*/div/*/.onsend
    src:x:/../*/.onsend/*
  add:x:/../*/return/*/div/*/.onclose
    src:x:/../*/.onclose/*

  /*
   * Adding contens of [oninit], if specified.
   */
  add:x:/../*/return/*/div/*/oninit
    src:x:/../*/oninit/*

  /*
   * Adding recipients to recipients widget.
   */
  add:x:/../*/return/**/sephia.widgets._recipients
    src:x:/../*/recipients/*

  /*
   * Adding attachments supplied by caller to email.
   */
  for-each:x:/../*/attachment
    eval-x:x:/+/*/*/*
    add:x:/../*/return/*/div/*/oninit
      src
        sephia.widgets.composer._add-attachment:x:/../*/_event?value
          filename:x:/@_dp/#?value

  /*
   * Returning actual widget to caller.
   */
  eval-x:x:/../*/return/**/widgets/=content/*(/input|/literal)/*/value|/../*/return/*/*/class
  return
    div
      oninit

        /*
         * In case widget is used from other apps, we make sure we include the necessary CSS files here.
         */
        p5.web.include-css-file:@MICRO/media/main.css
        p5.web.include-css-file:@MICRO/media/ext.css
        p5.web.include-css-file:@MICRO/media/fonts.css
        p5.web.include-css-file:@SEPHIA/media/main.css

      .onsend
      .onclose
      .email-composer
      class:x:(/../*/class|/@.defaults/*/class)/$?value
      events

        /*
         * Adds an attachment to your email.
         *
         * Requires [filename] as argument.
         */
        sephia.widgets.composer._add-attachment

          /*
           * Verifying this is our instance.
           */
          if:x:/../*/_arg?value
            !=:x:/../*/_event?value

            /*
             * Not our instance, returning early.
             */
            return

          /*
           * Finding main attachment wrapper widget for instance.
           */
          p5.web.widgets.find-first:x:/../*/_event?value
            class:sephia-attachments

          /*
           * Figuring out filename, without path.
           */
          split:x:/../*/filename?value
            =:/

          /*
           * Creating widget wrapping given attachment, such that it will be automatically serialized
           * when form is serialized.
           */
          create-widget
            class:sephia-attachment
            parent:x:/@p5.web.widgets.find-first/*/*?value
            widgets
              input
                type:hidden
                .data-field:attachment
                value:x:/../*/filename?value
              button
                innerValue:x:/@split/0/-?name
                onclick

                  /*
                   * Deleting file from tmp folder, and deleting widget wrapping attachment.
                   */
                  p5.web.widgets.get-parent:x:/../*/_event?value
                  micro.form.serialize:x:/-/*/*?value
                  delete-file:x:/@micro.form.serialize/*/attachment?value
                  delete-widget:x:/@p5.web.widgets.get-parent/*/*?value

      widgets:content

        /*
         * Subject of reply.
         */
        input
          type:text
          class:fill
          placeholder:Subject ...
          .data-field:subject
          value:x:/../*/subject?value
          oninit

            /*
             * Settings initial focus to "subject" of email.
             */
            micro.page.set-focus:x:/../*/_event?value

        /*
         * Body of reply.
         */
        literal
          element:textarea
          class:fill
          placeholder:Subject of email ...
          .data-field:body
          rows:15
          value:x:/../*/body?value

        /*
         * Wrapper for attachments.
         */
        container
          class:sephia-attachments

        /*
         * Our "select recipient" extension widget.
         */
        sephia.widgets._recipients

        /*
         * Button strip at bottom for "send", "discard", etc.
         */
        div
          class:right
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets
                button
                  innerValue:@"<span class=""icon-send""></span>"
                  style:"margin-bottom:0;"
                  title:Send email
                  onclick

                    /*
                     * Sending email.
                     *
                     * Making sure we wrap attempt in try/catch block, to give user friendly error messages.
                     */
                    try

                      /*
                       * Finding root composer widget, and serializing all form elements within it.
                       */
                      p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                        .email-composer
                      micro.form.serialize:x:/-/*/*?value

                      /*
                       * Making sure we HTML decode the body.
                       */
                      set:x:/@micro.form.serialize/*/body?value
                        p5.html.html-decode:x:/@micro.form.serialize/*/body?value

                      /*
                       * Looping through each attachment specified by user.
                       */
                      for-each:x:/@micro.form.serialize/*/attachment
                        eval-x:x:/+/*/*
                        add:x:/..try/*/sephia.core.send-email/*/attachments
                          src
                            file:x:/@_dp/#?value

                      /*
                       * Invoking Active Event responsible for sending email, passing in arguments
                       * from above serialization invocation.
                       */
                      add:x:/+
                        src:x:/@micro.form.serialize/*(!/attachment!/no-data)
                      sephia.core.send-email
                        attachments

                      /*
                       * Invoking specified [.onclose] and [.onsend] lambda objects given during creation of widget.
                       */
                      p5.web.widgets.ajax-events.raise:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                        .onsend
                        .onclose

                    catch

                      /*
                       * Oops, displaying error to user in a modal widget.
                       */
                      create-widgets
                        micro.widgets.modal:sephia-error-modal-widget
                          class:micro-modal micro-modal-smaller
                          widgets
                            h3
                              innerValue:Oops!
                            p
                              innerValue:Oops, email could not be sent. Message from system was; '{0}'
                                :x:/../*/catch/*/message?value
                            div
                              class:right
                              widgets
                                button
                                  innerValue:OK
                                  oninit
                                    micro.page.set-focus:x:/../*/_event?value
                                  onclick
                                    delete-widget:sephia-error-modal-widget

                input
                  style:"display:none;"
                  type:file
                  multiple
                  class:sephiaAttachmentFileInput
                  .data-field:no-data
                  .onupload

                    /*
                     * Retrieving the file name and its content, and saving it to user's 
                     * temporary attachment folder.
                     */
                    p5.web.post.get:sephia-file-name
                    p5.web.post.get:sephia-file-content
                    p5.string.decode-base64:x:/-/*?value
                    save-file:@SEPHIA-USER-ATTACHMENTS/tmp/{0}
                      :x:/../*/p5.web.post.get/[0,1]/*?value
                      src:x:/@p5.string.decode-base64?value

                    /*
                     * Adding attachment to email.
                     */
                    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                      .email-composer
                    eval-x:x:/+/*/*
                    add:x:/../*/sephia.widgets.composer._add-attachment
                      src
                        filename:@SEPHIA-USER-ATTACHMENTS/tmp/{0}
                          :x:/../*/p5.web.post.get/[0,1]/*?value
                    sephia.widgets.composer._add-attachment:x:/@p5.web.widgets.find-first-ancestor/*/*?value

                  onchange:"p5.sephia_upload_files(event);"

                button
                  innerValue:@"<span class=""icon-folder-open-o""></span>"
                  style:"margin-bottom:0;"
                  title:Add attachments
                  onclick:@"document.querySelector('.sephiaAttachmentFileInput').click();return false;"
                button
                  innerValue:@"<span class=""icon-trash-o""></span>"
                  style:"margin-bottom:0;"
                  title:Discard email
                  onclick

                    /*
                     * Invoking specified [.onclose] lambda object given during creation of widget.
                     */
                    p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                      .email-composer
                    p5.web.widgets.ajax-events.raise:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                      .onclose
