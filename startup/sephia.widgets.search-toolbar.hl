
/*
 * Creating Sephia toolbar extension widget.
 *
 * This widget can be used in any places where an extension widget can be used, such as for instance [widgets]
 * collections of other widgets, etc.
 *
 * Arguments;
 * - [databind-event]      - Active Event to raise when whatever "datagrid" you've associated your toolbar with
 *                           should be re-databound, due to e.g. "page" or "filter" criteria having changed.
 *                           This event might receive zero or more of the following arguments;
 *                           [filter] and [offset] being the "search query" and the "offset into dataset" to start
 *                           showing results from.
 * - [page-size]           - Size of your "page", meaning number of items to display at teh same time in your datagrid.
 * - [search-accesskey]    - Access key property for search textbox.
 */
create-event:sephia.widgets.search-toolbar

  /*
   * Basic sanity check!
   */
  if:x:/../*/databind-event?value
    not
    throw:@"The [sephia.widgets.search-toolbar] extension widget, requires at least a [databind-event] which it will raise 
whenever the associated 'datagrid' needs to be re-databound"

  /*
   * Applying argument(s).
   */
  set:x:/../*/return/**(/databind-event)?name
    src:x:/../*/databind-event?value
  eval-x:x:/../*/return/**(/accesskey|/_page-size)

  /*
   * Returning widget to caller.
   */
  return
    div
      class:input-group
      oninit

        /*
         * Making sure we trap carriage return, and click "search" button in "search" textbox.
         * First including JavaScript, which maps between textbox and search button.
         */
        p5.web.widgets.find:x:/../*/_event?value
          type:search
        p5.web.widgets.find:x:/../*/_event?value
          _search-button
        p5.web.include-javascript:@"p5.sephia_toolbar_search_keypress_{0} = function(e) {{
          if(e.keyCode == 13) {{
            p5.$('{1}').raise('onclick');
            return false;
          }}
        }}"
          :x:/../*/p5.web.widgets.find/[0,1]/*/*?value
          :x:/../*/p5.web.widgets.find/[1,2]/*/*?value

        /*
         * Then associating "onkeydown" DOM event with JavaScript function included above.
         */
        p5.web.widgets.property.set:x:/@p5.web.widgets.find/*/*?value
          onkeypress:"return p5.sephia_toolbar_search_keypress_{0}(event);"
            :x:/@p5.web.widgets.find/*/*?value

      widgets

        /*
         * Search textbox.
         */
        input
          type:search
          class:form-control
          placeholder:Search ...
          accesskey:x:/../*/search-accesskey?value

        span
          class:input-group-btn
          _offset:0
          widgets

            /*
             * Search button.
             */
            button
              _search-button
              class:btn btn-default
              innerValue:@"<span class=""glyphicon glyphicon-search""></span>"
              onclick

                /*
                 * Re-databinding datagrid, now with "search query".
                 *
                 * First getting search criteria as supplied by user.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  class:input-group
                p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  type:search
                p5.web.widgets.property.get:x:/@p5.web.widgets.find/*/*?value
                  value

                /*
                 * Notice, the Active event invocation below [databind-event] is dynamically changed in accordance 
                 * to [databind-event] argument supplied during creation of widget.
                 */
                eval-x:x:/+/*
                databind-event
                  filter:x:/@p5.web.widgets.property.get/*/*?value

                /*
                 * Then making sure we set "pager offset value" to zero.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  _offset
                p5.web.widgets.property.set:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  _offset:0

                /*
                 * To make it easier to search multiple times, we make sure we set focus to, 
                 * and select all text in search textbox.
                 */
                p5.web.send-javascript:@"$('#{0}').focus().select();"
                  :x:/@p5.web.widgets.find/*/*?value

            /*
             * Clear button.
             */
            button
              class:btn btn-default
              innerValue:@"<span class=""glyphicon glyphicon-remove""></span>"
              onclick

                /*
                 * Re-databinding datagrid, now without filter.
                 */
                databind-event

                /*
                 * Setting search textbox content to empty.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  class:input-group
                p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  type:search
                p5.web.widgets.property.set:x:/@p5.web.widgets.find/*/*?value
                  value:

                /*
                 * Setting current "offset" (page value) to zero.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  _offset
                p5.web.widgets.property.set:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  _offset:0

                /*
                 * To make it easier to search immediately, we make sure we set focus to, 
                 * and select all text in search textbox.
                 */
                p5.web.send-javascript:@"$('#{0}').focus().select();"
                  :x:/@p5.web.widgets.find/*/*?value

            /*
             * Previous "page" button.
             */
            button
              class:btn btn-default
              innerValue:@"<span class=""glyphicon glyphicon-chevron-left""></span>"
              onclick

                /*
                 * Verifying offset is more than 0, and if so, decrementing by 19, and re-databinding grid.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  _offset
                p5.web.widgets.property.get:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  _offset
                if:x:/@p5.web.widgets.property.get/*/*?value.int
                  <=:int:0

                  /*
                   * At the beginning of dataset.
                   */
                  sys42.windows.info-tip:No more pages.
                  return

                /*
                 * Subtracting number of items per page, and updating "offset" in ancestor widget.
                 */
                _page-size:x:/../*/page-size?value.int
                -:x:/@p5.web.widgets.property.get/*/*?value.int
                  _:x:/@_page-size?value
                p5.web.widgets.property.set:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  _offset:x:/@-?value.string

                /*
                 * Retrieving "search textbox" value.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  class:input-group
                p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  type:search
                p5.web.widgets.property.get:x:/@p5.web.widgets.find/*/*?value
                  value

                /*
                 * Figuring out how to invoke [databind-event] passed in during creation of widget.
                 * First checking if we need to pass in filter at all.
                 */
                if:x:/@p5.web.widgets.property.get/*/*?value

                  /*
                   * Fiter is necessary.
                   */
                  add:x:/../*/_signal/+
                    src
                      filter:x:/@p5.web.widgets.property.get/*/*?value

                /*
                 * Checking if we need to pass in offset
                 */
                if:x:/@-?value.int
                  >:int:0

                  /*
                   * Offset is necessary.
                   */
                  add:x:/../*/_signal/+
                    src
                      offset:x:/@-?value.int

                /*
                 * Invoking (parametrized) [databind-event] event.
                 */
                eval-x:x:/+2/*
                _signal
                databind-event

                /*
                 * Setting focus to search textbox, and making sure we select all text, to make it easier to supply
                 * a new search criteria immediately.
                 */
                p5.web.send-javascript:@"$('#{0}').focus().select();"
                  :x:/@p5.web.widgets.find/*/*?value

            button
              class:btn btn-default
              innerValue:@"<span class=""glyphicon glyphicon-chevron-right""></span>"
              onclick

                /*
                 * Adding number of items per page, and updating "offset" in ancestor widget.
                 */
                _page-size:x:/../*/page-size?value.int
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  _offset
                p5.web.widgets.property.get:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  _offset
                +:x:/@p5.web.widgets.property.get/*/*?value.int
                  _:x:/@_page-size?value
                p5.web.widgets.property.set:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  _offset:x:/@+?value.string

                /*
                 * Retrieving "search textbox" value.
                 */
                p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                  class:input-group
                p5.web.widgets.find:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                  type:search
                p5.web.widgets.property.get:x:/@p5.web.widgets.find/*/*?value
                  value

                /*
                 * Figuring out how to invoke [databind-event] passed in during creation of widget.
                 * First checking if we need to pass in filter at all.
                 */
                if:x:/@p5.web.widgets.property.get/*/*?value

                  /*
                   * Fiter is necessary.
                   */
                  add:x:/../*/_signal/+
                    src
                      filter:x:/@p5.web.widgets.property.get/*/*?value


                /*
                 * Invoking (parametrized) [databind-event] event.
                 */
                eval-x:x:/+2/*
                _signal
                databind-event
                  offset:x:/@+?value.int

                /*
                 * Setting focus to search textbox, and making sure we select all text, to make it easier to supply
                 * a new search criteria immediately.
                 */
                p5.web.send-javascript:@"$('#{0}').focus().select();"
                  :x:/@p5.web.widgets.find/*/*?value
