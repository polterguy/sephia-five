
/*
 * Creates the "settings help" widget Active Event.
 * Creates a fieldset, with help text, and an icon for toggling the help text. Passs in children widgets to your
 * fieldset widget as [widgets]. Any special events you wish to apply for your fieldset, can be passed in as [events].
 *
 * Arguments;
 *
 * [legend] - Text for legend.
 * [help] - Help text.
 * [widgets] - Widgets in fieldset.
 */
create-event:sephia.widgets.settings.fieldset

  /*
   * Applying arguments.
   */
  add:x:/../*/return/*/fieldset/*/widgets
    src:x:/../*/widgets/*
  eval-x:x:/../*/return/*/fieldset/*/widgets/*(/legend/*/widgets/*/span/*/innerValue|/literal/*/innerValue)

  /*
   * Returning fieldset widget.
   */
  return
    fieldset
      class:prepend-top
      widgets
        legend
          widgets
            span
              innerValue:x:/../*/legend?value

            /*
             * Button that toggles help text.
             */
            a
              href:#
              role:button
              innerValue:@"<span class=""glyphicon glyphicon-question-sign sephia-settings-help-button""></span>"
              onclick

                /*
                 * Toggling visibility of "help text".
                 */
                p5.web.widgets.get-parent:x:/../*/_event?value
                p5.web.widgets.get-parent:x:/-/*/*?value
                p5.web.widgets.find-like:x:/-/*/*?value
                  class:sephia-settings-help-info
                if
                  fetch:x:/0/0/0?value.bool
                    p5.web.widgets.property.get:x:/@p5.web.widgets.find-like/*/*?value
                      visible

                  /*
                   * Hiding help text.
                   */
                  p5.web.widgets.property.set:x:/@p5.web.widgets.find-like/*/*?value
                    visible:false
                else

                  /*
                   * Showing help text.
                   */
                  p5.web.widgets.property.set:x:/@p5.web.widgets.find-like/*/*?value
                    visible:true

        /*
         * Help information.
         */
        literal
          element:div
          class:col-xs-12 fade-in prepend-bottom sephia-settings-help-info
          visible:false
          innerValue:x:/../*/help?value
